/*
----------------------------
MetaDoom
Actor Definitions
Heavy Weapons Dude
----------------------------
*/

// Uggh, these fuckers. This one is more or less the same as he always was, but
// reskinned to use an Assault Rifle instead of a Chaingun.

class MetaMachineGunGuy : MonsterCore
{
	Default
	{
		Health 70;
		Radius 20;
		Height 56;
		Mass 100;
		Speed 8;
		PainChance 170;
		Monster;
		+FLOORCLIP
		+DOHARMSPECIES
		SeeSound "monsters/machinegunner/see";
		PainSound "monsters/machinegunner/pain";
		DeathSound "monsters/machinegunner/die";
		ActiveSound "monsters/machinegunner/idle";
		AttackSound "chainguy/attack";
		Obituary "$OB_MGUNGUY";
		Dropitem "MetaMachineGun";
		damagefactor "HolyWater", 0.25;
		painchance "HolyWater", 255;
		damagefactor "Axe", 50;
		Tag "Former Lieutenant";
		//$Category "Monsters/MetaDoom/Possessed/Heavy Weapons Dude"
		//$Title "Former Lieutenant"
		//$Color 12
	}
	
	States
	{
	Spawn:
		MPOS AB 10 A_Look;
		Loop;
	See:
		MPOS AABBCCDD 3 A_MetaChase;
		Loop;
	Missile:
		MPOS E 10 A_FaceTarget;
		MPOS FE 4 BRIGHT 
		{
			A_PlaySound ("weapons/machinegun/fire");
			A_SpawnProjectile("MonsterZombiemanTracer",32,0,random(-4,4),CMF_OFFSETPITCH,random(-4,4));
		}
		MPOS F 1 A_CPosRefire;
		Goto Missile+1;
	Pain:
		MPOS G 0;
		MPOS G 3;
		MPOS G 3 A_Pain;
		Goto See;
	Punched:
		MPOS G 3;
		MPOS G 12 A_Pain;
		Goto See;
	Death:
		MPOS H 0 { bSPRITEFLIP = random(0,1); } // random chance of sprite flipping
		TNT1 A 0 A_SpawnItem("SoulSpawner");
		MPOS H 5 A_GiveToTarget("ScoreItem", 300);
		MPOS I 5 A_Scream;
		MPOS J 5 A_NoBlocking;
		MPOS KL 5;
		MPOS M -1 A_PlaySound("bodyfalls/medium");
		Stop;
	XDeath:
		TNT1 A 0 A_SpawnItem("SoulSpawner");
		CPOS O 5 A_GiveToTarget("ScoreItem", 300);
		"####" # 0 A_Fall;
		TNT1 A 0 A_SpawnItemEx("MetaGibber",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS);
		Stop;
	Raise:
		MPOS M 5 { bSPRITEFLIP = 0; }
		MPOS LKJIH 5;
		Goto See;
	}
}

// The upgraded version. Takes a little while to spin up, but fires at a far
// greater firing rate (and a much lower accuracy) to keep things interesting.

class MetaChainGunGuy : MonsterCore
{
	Default
	{
		Health 80;
		Radius 20;
		Height 56;
		Mass 100;
		Speed 8;
		PainChance 170;
		Monster;
		+FLOORCLIP
		+DOHARMSPECIES
		SeeSound "monsters/machinegunner/see";
		PainSound "monsters/machinegunner/pain";
		DeathSound "monsters/machinegunner/die";
		ActiveSound "monsters/machinegunner/idle";
		AttackSound "chainguy/attack";
		Obituary "$OB_CGUNGUY";
		Dropitem "MetaChainGun";
		damagefactor "HolyWater", 0.25;
		painchance "HolyWater", 255;
		damagefactor "Axe", 50;
		Tag "Former Commando";
		//$Category "Monsters/MetaDoom/Possessed/Heavy Weapons Dude"
		//$Title "Former Commando"
		//$Color 12
	}
	
	States
	{
	Spawn:
		CPOS AB 10 A_Look;
		Loop;
	See:
		CPOS AABBCCDD 3 A_MetaChase;
		Loop;
	Missile:
		CPOS E 0 A_PlaySound("weapons/chaingun/windup", CHAN_WEAPON);
		CPOS EEEEEEEEEEEEEEEEEEEEEEEEEEE 1 A_FaceTarget;
	Brapp:
		CPOS FE 1 BRIGHT 
		{
			A_PlaySound ("weapons/chaingun/fire", CHAN_WEAPON);
			A_SpawnProjectile("MonsterCommandoTracer",32,0,random(-16,16),CMF_OFFSETPITCH,random(-4,4));
		}
		CPOS F 1 A_MonsterRefire (10, "WindDown");
		loop;
	Winddown:
		CPOS E 15 A_PlaySound("weapons/chaingun/winddown", CHAN_WEAPON);
		goto See;
	Pain:
		CPOS G 0;
		CPOS G 0 A_StopSound(CHAN_WEAPON);
		CPOS G 3;
		CPOS G 3 A_Pain;
		Goto See;
	Punched:
		CPOS G 3;
		CPOS G 12 A_Pain;
		Goto See;
	Death:
		CPOS H 0 { bSPRITEFLIP = random(0,1); } // random chance of sprite flipping
		TNT1 A 0 A_SpawnItem("SoulSpawner");
		CPOS G 0 A_StopSound(CHAN_WEAPON);
		CPOS H 5 A_GiveToTarget("ScoreItem", 300);
		CPOS I 5 A_Scream;
		CPOS J 5 A_NoBlocking;
		CPOS KLM 5;
		CPOS N -1 A_PlaySound("bodyfalls/medium");
		Stop;
	XDeath:
		TNT1 A 0 A_SpawnItem("SoulSpawner");
		CPOS G 0;
		CPOS G 0 A_StopSound(CHAN_WEAPON);
		CPOS O 5 A_GiveToTarget("ScoreItem", 300);
		"####" # 0 A_Fall;
		TNT1 A 0 A_SpawnItemEx("MetaGibber",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS);
		Stop;
	Raise:
		CPOS N 5 { bSPRITEFLIP = 0; }
		CPOS MLKJIH 5;
		Goto See;
	}
}

// Former Assassin, aka The Doom 3 Chaingunner. Smarter, and has a tentacle pull
// attack powered, yet again, by Jekyll Grim Payne's Grappling Hook code.

class MetaAssassinGuy : MonsterCore
{
	Default
	{
		Health 80;
		Radius 20;
		Height 56;
		Mass 100;
		Speed 8;
		PainChance 170;
		Monster;
		+FLOORCLIP
		+DOHARMSPECIES
		SeeSound "monsters/d3chaingunner/see";
		PainSound "monsters/d3chaingunner/pain";
		DeathSound "monsters/d3chaingunner/die";
		ActiveSound "monsters/d3chaingunner/idle";
		AttackSound "chainguy/attack";
		Obituary "$OB_D3CGUNGUY";
		Dropitem "MetaChainGun";
		damagefactor "HolyWater", 0.25;
		painchance "HolyWater", 255;
		damagefactor "Axe", 50;
		Tag "Former Assassin";
		//$Category "Monsters/MetaDoom/Possessed/Heavy Weapons Dude"
		//$Title "Former Assassin"
		//$Color 12
	}
	
	AssassinTentacle hook;
	bool hookFired;
	bool draggingPlayer;
	
	vector2 FindHooktargetOfs(int atkdist = 320)
	{
		vector2 ofs;			
		double closestDist = double.infinity;
		actor hooktarget;
		BlockThingsIterator itr = BlockThingsIterator.Create(self,atkdist);
		while (itr.next())
		{
			let next = itr.thing;
			if (next == self)
				continue; 
			if (!next.bShootable || !(next.bIsMonster || (next is "PlayerPawn")))
				continue;
			double dist = self.Distance3D(next);
			if (dist > atkdist)
				continue;
			if (dist < closestDist)
				closestDist = dist;
			if (!self.CheckSight(next,SF_IGNOREWATERBOUNDARY))
				continue;
			vector3 targetpos = LevelLocals.SphericalCoords((self.pos.x,self.pos.y,self.pos.z+32),next.pos+(0,0,next.default.height*0.5),(self.angle,self.pitch));	
			if (abs(targetpos.x) > 11 || abs(targetpos.y) > 11) {
				continue;
			}
			ofs = targetpos.xy;
		}
		return ofs;
	}
	
	States
	{
	Spawn:
		PSCM AB 10 A_Look;
		Loop;
	See:
		PSCM AABBCCDD 3 A_MetaChase;
		Loop;
	Missile:
		PSCM E 0 A_Jump(256, "MissileTentacle"); // 64
		PSCM E 0 A_PlaySound("weapons/chaingun/windup", CHAN_WEAPON);
		PSCM EEEEEEEEEEEEEE 1 A_FaceTarget;
	Brapp:
		PSCM FE 1 BRIGHT 
		{
			A_PlaySound ("weapons/chaingun/fire", CHAN_WEAPON);
			A_SpawnProjectile("MonsterCommandoTracer",32,0,random(-16,16),CMF_OFFSETPITCH,random(-4,4));
		}
		PSCM F 1 A_MonsterRefire (80, "WindDown");
		loop;
	Winddown:
		PSCM E 10 A_PlaySound("weapons/chaingun/winddown", CHAN_WEAPON);
		goto See;
	MissileTentacle:
		PSCM A 5 A_FaceTarget;
		PSCM E 5 A_FaceTarget;
		PSCM G 0 //A_SpawnProjectile ("AssassinTentacle", 32, 0, 0);
		{
			A_Stop();
			A_FaceTarget();
			hook = AssassinTentacle(A_SpawnProjectile ("AssassinTentacle", 32, 0, 0));
		}
	MissileTongueLoop:
		PSCM G 1
		{
			if (!hook)
			{
				SetStateLabel("MissileTentacleEnd");
			}
		}
		loop;
	MissileTentacleEnd:
		PSCM EA 5 A_FaceTarget;
		Goto See;
	Pain:
		PSCM H 3
		{
			A_StopSound(CHAN_WEAPON);
			
			if (hook)
			{
				hook.A_Remove(AAPTR_DEFAULT, RMVF_EVERYTHING);
			}
		}
		PSCM H 3 A_Pain;
		Goto See;
	Punched:
		PSCM H 3;
		PSCM H 12 A_Pain;
		Goto See;
	Death:
		PSCM I 5
		{
			if (hook)
			{
				hook.A_Remove(AAPTR_DEFAULT, RMVF_EVERYTHING);
			}
			
			bSPRITEFLIP = random(0,1); // random chance of sprite flipping
			A_SpawnItem("SoulSpawner");
			A_StopSound(CHAN_WEAPON);
			A_GiveToTarget("ScoreItem", 300);
		}
		PSCM J 5 A_Scream;
		PSCM K 5 A_NoBlocking;
		PSCM L 5 A_PlaySound("bodyfalls/medium");
		PSCM MN 5;
		PSCM O -1;
		Stop;
	XDeath:
		PSCM P 5 
		{
			if (hook)
			{
				hook.A_Remove(AAPTR_DEFAULT, RMVF_EVERYTHING);
			}
			
			A_SpawnItem("SoulSpawner");
			A_StopSound(CHAN_WEAPON);
			A_GiveToTarget("ScoreItem", 300);
		}
		"####" # 0 A_Fall;
		TNT1 A 0 A_SpawnItemEx("MetaGibber",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS);
		Stop;
	Raise:
		PSCM O 5 { bSPRITEFLIP = 0; }
		PSCM NMLKJI 5;
		Goto See;
	}
}

Class AssassinTentacle : Actor
{
	Default
	{
		AssassinTentacle.maxdistance 420;
		projectile;
		damage 0;
		speed 15;
		+HITTRACER
		+BLOODLESSIMPACT
		SeeSound "monsters/d3chaingunner/tentacle";
	}
	
	vector3 spawnPos;
	int maxdistance;
	int flytime;
	MetaHookControl control;
	property maxdistance : maxdistance;
	bool roadback;
	float soundpitch;
	
	override void PostBeginPlay()
	{
		super.PostBeginPlay();
		spawnPos = pos;
		A_StartSound("monsters/wretched/tongueloop", 333, CHANF_LOOP, 0.25, ATTN_NORM, 1.0);
		soundpitch = 1.0;
	}
	
	override void Tick()
	{
		super.Tick();
		
		if (isFrozen())
			return;
			
		if (target)
			A_CustomRailgun (0, 0, 0, 0, RGF_SILENT|RGF_NOPIERCING|RGF_CENTERZ, 1, 0, "MetaNothingPuff", 0, 0, 4096, 1, 32, 0, "AssassinTentacleTrail", -4);
		
		if (level.Vec3Diff(pos, spawnPos).length() > maxdistance && !inStateSequence(curstate,FindState("Flyback")))
			SetStateLabel("Flyback");
			
		if (roadback)
		{
			soundpitch = soundpitch - 0.1;
		} else {
			soundpitch = soundpitch + 0.1;
		}
		
		A_SoundPitch(333, soundpitch);
	}
	
	states
	{
	Spawn:
		CTNG A 1;
		loop;
	Death:
		CTNG A 0 A_StartSound("monsters/wretched/hitwall");
		/*
		WTNG A 1
		{
			if (target && target.player) {
				target.speed = 0;
				target.vel = target.Vec3To(self).Unit() * 25;
				if (Distance3D(target) <= 64) {
					target.speed = target.default.speed;					
					destroy();
				}
			}
		}
		wait;
		*/
		goto Flyback;
	XDeath:
		CTNG A 0 A_Scream();
		CTNG A 1
		{
			if (tracer) {
				tracer.GiveInventory("MetaHookControl",1);
				control = MetaHookControl(tracer.FindInventory("MetaHookControl"));
				control.master = self;
			}
		}
		goto Flyback;
	Flyback:
		CTNG A 1
		{
			if (target)
			{
				roadback = true;
				flytime++;
				vel = Vec3To(target).Unit() * 30;
				if (Distance3D(target) <= 64 || flytime > 35*5) {
					if (tracer)
						tracer.TakeInventory("MetaHookControl",1);						
					destroy();
				}
			}
		}
		loop;
	}
	
	override int SpecialMissileHit (Actor victim)
	{		
		if ((victim.bISMONSTER == true || victim is "PlayerPawn") && victim.health > 0 && victim != target)
		{
			victim.A_StartSound("monsters/wretched/pull", 13);
		}
		
		return -1;
	}
}

/*
class AssassinTentacle : LoreShot
{
	Default
	{
		Speed 15;
		Height 14;
		Radius 10;
		Projectile;
		//+STRIFEDAMAGE
		Damage 2;
		MaxStepHeight 4;
		SeeSound "monsters/d3chaingunner/tentacle";
	}
	
	States
	{
	Spawn:
		CTNG AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItem("AssassinTentacleTrail");
		stop;
	Death:
		CTNG A 0 A_PlaySound("monsters/wretched/pull");
		Stop;
	}
}
*/

class AssassinTentacleTrail : Actor
{
	Default
	{
		+NOBLOCKMAP
		+NOGRAVITY
		scale 0.8;
	}
	states
	{
	Spawn:
		CTNG A 0;
		CTNG A 1;
		stop;
	}
}