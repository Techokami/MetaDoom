/*
----------------------------
MetaDoom
Actor Definitions
Shotgun Guy
----------------------------
*/

// The original enemy, converted to use tracer projectiles instead of hitscans.
// Nothing more, nothing less.

class MetaShotgunGuy : MonsterCore
{
	Default
	{
		Health 30;
		Radius 20;
		Height 56;
		Mass 100;
		Speed 8;
		PainChance 170;
		Monster;
		+FLOORCLIP
		+DOHARMSPECIES
		SeeSound "monsters/shotgunguy/see";
		AttackSound "monsters/shotgunguy/fire";
		PainSound "monsters/shotgunguy/pain";
		DeathSound "monsters/shotgunguy/die";
		ActiveSound "monsters/shotgunguy/idle";
		Obituary "$OB_SHOTGUY";
		DropItem "MetaShotgun";
		damagefactor "HolyWater", 0.25;
		painchance "HolyWater", 255;
		damagefactor "Axe", 50;
		Tag "Shotgun Guy";
		//$Category "Monsters/MetaDoom/Possessed/Shotgun Guy"
		//$Title "Shotgun Guy"
		//$Color 12
	}
	
	States
	{
	Spawn:
		SPOS AB 10 A_Look;
		Loop;
	See:
		SPOS AABBCCDD 3 A_MetaChase;
		Loop;
	Missile:
		SPOS E 10 A_FaceTarget;
		SPOS F 10 BRIGHT
		{
			A_SpawnProjectile("MonsterShotgunGuyTracer",32,0,random(-16,16),CMF_OFFSETPITCH,random(-4,4));
			A_SpawnProjectile("MonsterShotgunGuyTracer",32,0,random(-16,16),CMF_OFFSETPITCH,random(-4,16));
			A_SpawnProjectile("MonsterShotgunGuyTracer",32,0,random(-16,16),CMF_OFFSETPITCH,random(-4,4));
			A_SpawnProjectile("MonsterShotgunGuyTracer",32,0,random(-2,2),CMF_OFFSETPITCH,random(-2,2));
			A_PlaySound("monsters/shotgunguy/fire");
		}
		SPOS E 10;
		Goto See;
	Pain:
		SPOS G 0;
		SPOS G 3;
		SPOS G 3 A_Pain;
		Goto See;
	Punched:
		SPOS G 3;
		SPOS G 3;
		SPOS G 12 A_Pain;
		Goto See;
	Death:
		SPOS H 0 { bSPRITEFLIP = random(0,1); } // random chance of sprite flipping
		TNT1 A 0 A_SpawnItem("SoulSpawner");
		SPOS H 5 A_GiveToTarget("ScoreItem", 150);
		SPOS I 5 A_Scream;
		SPOS J 5 A_NoBlocking;
		SPOS K 5;
		SPOS L -1 A_PlaySound("bodyfalls/medium");
		Stop;
	XDeath:
		TNT1 A 0 A_SpawnItem("SoulSpawner");
		SPOS M 5 A_GiveToTarget("ScoreItem", 150);
		"####" # 0 A_Fall;
		TNT1 A 0 A_SpawnItemEx("MetaGibber",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS);
		Stop;
	Raise:
		SPOS L 5 { bSPRITEFLIP = 0; }
		SPOS KJIH 5;
		Goto See;
	}
}

// ZSEC from Doom 3. This variant has a ROTT/Goldeneye style Dodge Roll.

class MetaZSec : MonsterCore
{
	Default
	{
		Health 50;
		Radius 20;
		Height 56;
		Mass 100;
		Speed 10;
		PainChance 170;
		Monster;
		+FLOORCLIP
		+DOHARMSPECIES
		+ROLLSPRITE
		+ROLLCENTER
		SeeSound "monsters/zsec/see";
		AttackSound "shotguy/attack";
		PainSound "monsters/zsec/pain";
		DeathSound "monsters/zsec/die";
		ActiveSound "monsters/zsec/idle";
		Obituary "$OB_ZSEC";
		DropItem "MetaShotgun";
		damagefactor "HolyWater", 0.25;
		painchance "HolyWater", 255;
		damagefactor "Axe", 50;
		Tag "Z-Sec";
		//$Category "Monsters/MetaDoom/Possessed/Shotgun Guy"
		//$Title "Z-Sec"
		//$Color 12
	}
	
	States
	{
	Spawn:
		ZSEC AB 10 A_Look;
		Loop;
	See:
		ZSEC AABBCCDD 3 A_MetaChase();
		Loop;
	Missile:
		ZPOC E 0 A_Jump(128, "Roll");
		ZSEC E 10 A_FaceTarget();
		ZSEC F 10 BRIGHT
		{
			A_SpawnProjectile("MonsterShotgunGuyTracer",32,0,random(-16,16),CMF_OFFSETPITCH,random(-4,4));
			A_SpawnProjectile("MonsterShotgunGuyTracer",32,0,random(-16,16),CMF_OFFSETPITCH,random(-4,16));
			A_SpawnProjectile("MonsterShotgunGuyTracer",32,0,random(-16,16),CMF_OFFSETPITCH,random(-4,4));
			A_SpawnProjectile("MonsterShotgunGuyTracer",32,0,random(-2,2),CMF_OFFSETPITCH,random(-2,2));
			A_PlaySound("monsters/shotgunguy/fire");
		}
		ZSEC E 10;
		Goto See;
	Roll:
		ZPOC E 0 A_FaceTarget;
		ZPOC E 0 A_PlaySound("monsters/zsec/roll");
		ZPOC E 0 A_Jump(128, "RollLeft");
		goto RollRight;
	RollLeft:
		ZPOC E 0 ThrustThing(angle*256/360+192, 12, 0, 0);
		ZPOC EEEEEEEEEEEEEEEEE 1 A_SetRoll((roll + 20));
		goto CrouchMissile;
	RollRight:
		ZPOC E 0 ThrustThing(angle*256/360+64, 12, 0, 0);
		ZPOC EEEEEEEEEEEEEEEEE 1 A_SetRoll((roll - 20));
		goto CrouchMissile;
	CrouchMissile:
		ZPOC E 0 A_SetRoll(0);
		ZPOC E 15 A_FaceTarget;
		ZPOC F 10 BRIGHT
		{
			A_FaceTarget();
			A_SpawnProjectile("MonsterShotgunGuyTracer",16,0,random(-16,16),CMF_OFFSETPITCH,random(-4,4));
			A_SpawnProjectile("MonsterShotgunGuyTracer",16,0,random(-16,16),CMF_OFFSETPITCH,random(-4,16));
			A_SpawnProjectile("MonsterShotgunGuyTracer",16,0,random(-16,16),CMF_OFFSETPITCH,random(-4,4));
			A_SpawnProjectile("MonsterShotgunGuyTracer");
			A_PlaySound("monsters/shotgunguy/fire");
		}
		ZPOC E 10;
		Goto See;
	Pain:
		ZSEC G 0;
		ZSEC G 3 A_SetRoll(0);
		ZSEC G 3 A_Pain;
		Goto See;
	Punched:
		ZSEC G 3 A_SetRoll(0);
		ZSEC G 12 A_Pain;
		Goto Roll;
	Death:
		ZSEC H 0 A_SetRoll(0);
		ZSEC H 0 { bSPRITEFLIP = random(0,1); } // random chance of sprite flipping
		TNT1 A 0 A_SpawnItem("SoulSpawner");
		ZSEC H 5 A_GiveToTarget("ScoreItem", 150);
		ZSEC I 5 A_Scream;
		ZSEC J 5 A_NoBlocking;
		ZSEC K 5;
		ZSEC L -1 A_PlaySound("bodyfalls/medium");
		Stop;
	XDeath:
		TNT1 A 0 A_SpawnItem("SoulSpawner");
		SPOS M 5 A_GiveToTarget("ScoreItem", 150);
		"####" # 0 A_Fall;
		TNT1 A 0 A_SpawnItemEx("MetaGibber",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS);
		Stop;
	Raise:
		ZSEC L 5 { bSPRITEFLIP = 0; }
		ZSEC KJIH 5;
		Goto See;
	}
	
	override void Tick()
	{
		if (meta_mapbreakers == 0)
		{
			bNODROPOFF = 1;
		} else {
			bNODROPOFF = 0;
		}
		
		Super.Tick();
	}
}