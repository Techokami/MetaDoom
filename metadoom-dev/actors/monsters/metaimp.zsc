/*
----------------------------
MetaDoom
Actor Definitions
Imp
----------------------------
*/

// The original. Basically the same as he was in 1993. Possibly earlier.

class MetaDoomImp : MonsterCore
{
	Default
	{
		Health 60;
		Radius 20;
		Height 56;
		Mass 100;
		Speed 8;
		PainChance 200;
		Monster;
		+FLOORCLIP
		SeeSound "monsters/imp/see";
		PainSound "monsters/imp/pain";
		DeathSound "monsters/imp/die";
		ActiveSound "monsters/imp/idle";
		HitObituary "$OB_IMPHIT";
		Obituary "$OB_IMP";
		painchance "HolyWater", 255;
		Tag "Imp";
		//$Category "Monsters/MetaDoom/Imp"
		//$Title "Imp"
		//$Color 12
	}

	States
	{
	Spawn:
		TROO AB 10 A_Look;
		Loop;
	See:
		TROO AABBCCDD 3 A_MetaChase;
		Loop;
	Melee:
	Missile:
		TROO EF 8 A_FaceTarget;
		TROO G 6 A_CustomComboAttack("MetaDoomImpBall", 32, 3 * random(1, 8), "monsters/imp/claw");
		Goto See;
	Pain:
		TROO H 0;
		TROO H 2;
		TROO H 2 A_Pain;
		Goto See;
	Punched:
		TROO G 3;
		TROO G 12 A_Pain;
		Goto See;
	Death:
		TROO I 0 { bSPRITEFLIP = random(0,1); } // random chance of sprite flipping
		TNT1 A 0 A_SpawnItem("SoulSpawner");
		TROO I 8 A_GiveToTarget("ScoreItem", 150);
		TROO J 8 A_Scream;
		TROO K 6;
		TROO L 6 A_NoBlocking;
		TROO M -1 A_PlaySound("bodyfalls/medium");
		Stop;
	XDeath:
		TNT1 A 0 A_SpawnItem("SoulSpawner");
		TROO N 5 A_GiveToTarget("ScoreItem", 150);
		TNT1 A 0 A_SpawnItemEx("MetaGibber",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS);
		Stop;
	Raise:
		TROO M 0 { bSPRITEFLIP = 0; }
		TROO MLKJI 8;
		Goto See;
	}
}

class MetaDoomImpBall : Actor
{
	Default
	{
		Radius 6;
		Height 8;
		Speed 10;
		FastSpeed 20;
		Damage 3;
		Projectile;
		+RANDOMIZE
		RenderStyle "Add";
		Alpha 1;
		SeeSound "monsters/imp/shoot";
		DeathSound "monsters/imp/fireball/hit";
		decal "DoomImpScorch";
	}

	States
	{
	Spawn:
		BAL1 A 0;
		BAL1 A 0 A_PlaySound("monsters/imp/fireball/loop", CHAN_BODY, 1.0, 1);
		BAL1 A 0 A_JumpIfInventory("ExtinguisherToken",1,"Extinguished");
		BAL1 A 4 BRIGHT A_SpawnItem ("DoomImpBallEffect", 0, 0, 0, 0);
		BAL1 B 0 A_JumpIfInventory("ExtinguisherToken",1,"Extinguished");
		BAL1 B 4 BRIGHT A_SpawnItem ("DoomImpBallEffect", 0, 0, 0, 0);
		Loop;
	Death:
		BAL1 CDE 6 BRIGHT;
		Stop;
	Extinguished:
		BAL1 A 0 A_PlaySound("monsters/lostsoul/extinguish");
		TNT1 AAAAA 1 A_SpawnItem ("DoomImpBallEffect", 0, 0, 0, 0);
		stop;
	}
}

class DoomImpBallEffect : Actor
{
	Default
	{
		+NOINTERACTION
		+NOGRAVITY
		Radius 0;
		Height 1;
		Speed 0;
		PROJECTILE;
		RenderStyle "Add";
		alpha 0.85;
		scale 0.65;
	}

	States
	{
	Spawn:
		RPUF A 2 Bright;
		RPUF ABCDE 2 Bright;
		Stop;
	}
} 

// The Nightmare Imp from Doom 64. Purple, semi-transparent, fast, makes you hate
// your life. Any questions?

class MetaNightmareImp : MonsterCore
{
	Default
	{
		Health 60;
		Radius 20;
		Height 56;
		Mass 100;
		Speed 16;
		FastSpeed 16;
		PainChance 200;
		scale 0.75;
		yscale 0.65;
		Monster;
		+FLOORCLIP
		-NEVERFAST
		SeeSound "monsters/nightmareimp/see";
		PainSound "monsters/nightmareimp/pain";
		DeathSound "monsters/nightmareimp/die";
		ActiveSound "monsters/nightmareimp/act";
		HitObituary "$OB_NIGHTMAREIMPHIT";
		Obituary "$OB_NIGHTMAREIMP";
		renderstyle "Translucent";
		alpha 0.75;
		painchance "HolyWater", 255;
		Tag "Nightmare Imp";
		//$Category "Monsters/MetaDoom/Imp"
		//$Title "Nightmare Imp"
		//$Color 12
	}

	States
	{
	Spawn:
		2ROO AB 10 A_Look;
		Loop;
	See:
		2ROO AABBCCDD 3 A_MetaChase;
		Loop;
	Melee:
		2ROO EF 8 A_FaceTarget;
		2ROO G 6 A_CustomComboAttack("MetaNightmareImpBall", 32, 3 * random(1, 8), "monsters/nightmareimp/claw");
		Goto See;
	Missile:
		2ROO IJ 4 A_FaceTarget;
		2ROO K 4 A_CustomComboAttack("MetaNightmareImpBall", 32, 3 * random(1, 8), "monsters/nightmareimp/claw");
		Goto See;
	Pain:
		2ROO H 0;
		2ROO H 2;
		2ROO H 2 A_Pain;
		Goto See;
	Death:
		2ROO L 0 { bSPRITEFLIP = random(0,1); } // random chance of sprite flipping
		2ROO L 8 A_GiveToTarget("ScoreItem", 250);
		2ROO M 8 A_Scream;
		2ROO N 6;
		2ROO O 6 A_NoBlocking;
		2ROO P -1 A_PlaySound("bodyfalls/medium");
		Stop;
	XDeath:
		TNT1 A 0 A_SpawnItem("SoulSpawner");
		2ROO Q 5 A_GiveToTarget("ScoreItem", 250);
		TNT1 A 0 A_SpawnItemEx("MetaGibber",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS);
		Stop;
	Raise:
		2ROO P 0 { bSPRITEFLIP = 0; }
		2ROO PONML 8;
		Goto See;
	}
}

class MetaNightmareImpBall : Actor
{
	Default
	{
		Radius 6;
		Height 8;
		Speed 20;
		FastSpeed 20;
		Damage 3;
		Projectile;
		+RANDOMIZE
		RenderStyle "Add";
		Alpha 1;
		scale 0.6;
		SeeSound "monsters/nightmareimp/shoot";
		DeathSound "monsters/nightmareimp/explode";
		decal "DoomImpScorch";
	}

	States
	{
	Spawn:
		BAL3 A 0;
		BAL3 A 0 A_PlaySound("monsters/imp/fireball/loop", CHAN_BODY, 1.0, 1);
		BAL3 A 0 A_JumpIfInventory("ExtinguisherToken",1,"Extinguished");
		BAL3 A 4 BRIGHT A_SpawnItem ("DoomImpBallEffect", 0, 0, 0, 0);
		BAL3 B 0 A_JumpIfInventory("ExtinguisherToken",1,"Extinguished");
		BAL3 B 4 BRIGHT A_SpawnItem ("DoomImpBallEffect", 0, 0, 0, 0);
		Loop;
	Death:
		BAL3 CDE 6 BRIGHT;
		Stop;
	Extinguished:
		BAL1 A 0 A_PlaySound("monsters/lostsoul/extinguish");
		TNT1 AAAAA 1 A_SpawnItem ("DoomImpBallEffect", 0, 0, 0, 0);
		stop;
	}
}

// Imp Lord - A Doom 4 Imp that is faster and has the teleport trick from the "Doom 4 1.0" footage

class MetaDoomImpLord : MonsterCore
{
	Default
	{
		Health 80;
		Radius 20;
		Height 56;
		Mass 100;
		Speed 10;
		PainChance 200;
		Monster;
		+FLOORCLIP
		SeeSound "monsters/implord/see";
		PainSound "monsters/implord/pain";
		DeathSound "monsters/implord/die";
		ActiveSound "monsters/implord/idle";
		HitObituary "$OB_IMPLORDHIT";
		Obituary "$OB_IMPLORD";
		painchance "HolyWater", 255;
		Tag "Imp Lord";
		//$Category "Monsters/MetaDoom/Imp"
		//$Title "Imp Lord"
		//$Color 12
	}
	
	int tries;

	States
	{
	Spawn:
		ROAC A 0;
		ROAC A 0 { bFLOAT = 0; bNOGRAVITY = 0; }
		ROAC AA 10 A_Look;
		Loop;
	See:
		ROAC BBCCDDEE 2 A_MetaChase;
		ROAC A 0 A_Jump(32, "TeleStart");
		Loop;
	Melee:
		//ROAC A 0 A_Jump(200, "TeleStartAway")
		ROAC F 8 A_FaceTarget;
		ROAC G 6 A_CustomComboAttack("MetaDoomImpBall", 32, 3 * random(1, 8), "monsters/imp/claw");
		Goto TeleStartAway;
	Missile:
		ROAC A 0 A_Jump(128, "TeleStart");
		ROAC A 0 A_Jump(64, "TeleStartAway");
		ROAC F 8 A_FaceTarget;
		ROAC G 6 A_CustomComboAttack("MetaDoomImpBall", 32, 3 * random(1, 8), "monsters/imp/claw");
		Goto See;
	TeleStart:
		ROAC A 0 A_JumpIf(meta_mapbreakers == 1, "TeleStartFull");
		goto TeleStartHalf;
	TeleStartFull:
		ROAC A 0 A_PlaySound("monsters/implord/jump", CHAN_VOICE);
		ROAC A 2 A_FaceTarget;
		ROAC A 2 { ThrustThingZ(0,48,0,0); ThrustThing(angle*256/360, 8, 0, 0); }
		ROAC FH 4;
		ROAC A 0 { A_PlaySound("monsters/implord/tele"); A_SpawnItem("MetaImpLordTeleportFog"); }
		ROAC A 0 { bFLOAT = 1; bNOGRAVITY = 1; }
		goto TeleLoop;
	TeleStartHalf:
		ROAC A 0 A_PlaySound("monsters/implord/jump", CHAN_VOICE);
		ROAC A 2 A_FaceTarget;
		ROAC A 2 { ThrustThingZ(0,16,0,0);  }
		ROAC FH 4;
		ROAC A 0 { A_PlaySound("monsters/implord/tele"); A_SpawnItem("MetaImpLordTeleportFog"); }
		ROAC A 0 { A_Warp(AAPTR_DEFAULT,0,0,0,0,WARPF_TOFLOOR); }
		goto TeleLoop;
	TeleStartAway:
		ROAC A 0 A_JumpIf(meta_mapbreakers == 1, "TeleStartAwayFull");
		goto TeleStartAwayHalf;
	TeleStartAwayFull:
		ROAC A 0 A_PlaySound("monsters/implord/jump", CHAN_VOICE);
		ROAC A 2 A_SetAngle(angle-random(150,220));
		ROAC A 2 { ThrustThingZ(0,48,0,0); ThrustThing(angle*256/360, 8, 0, 0); }
		ROAC FH 4;
		ROAC A 0 { A_PlaySound("monsters/implord/tele"); A_SpawnItem("MetaImpLordTeleportFog"); }
		ROAC A 0 { bFLOAT = 1; bNOGRAVITY = 1; }
		goto TeleLoopAway;
	TeleStartAwayHalf:
		ROAC A 0 A_PlaySound("monsters/implord/jump", CHAN_VOICE);
		ROAC A 2 A_FaceTarget;
		ROAC A 2 A_SetAngle(angle-random(150,220));
		ROAC A 2 { ThrustThingZ(0,16,0,0);  }
		ROAC FH 4;
		ROAC A 0 { A_PlaySound("monsters/implord/tele"); A_SpawnItem("MetaImpLordTeleportFog"); }
		ROAC A 0 { A_Warp(AAPTR_DEFAULT,0,0,0,0,WARPF_TOFLOOR); }
		goto TeleLoopAway;
	TeleLoop:
		TNT1 AAAAAAAAAAA 0 A_Chase(null, null);
		TNT1 AAAAAAAAAAA 0 A_Wander;
		TNT1 A 0
		{
			if (level.PointInSector(self.pos.xy).damageamount != 0)
			{
				if (tries < 10)
				{
					if (meta_developer)
						Console.printf("Imp Lord at %i x %i declined to teleport into nukage", self.pos.x, self.pos.y);
					tries++;
					SetStateLabel("TeleLoop");
				}
			}
		}
		TNT1 A 0 A_Jump(128, "TeleStop");
		loop;
	TeleLoopAway:
		TNT1 A 0 { bFRIGHTENED = 1; }
		TNT1 AAAAAAAAAAA 0 A_Chase(null, null);
		TNT1 AAAAAAAAAAA 0 A_Chase(null, null);
		TNT1 A 0
		{
			if (level.PointInSector(self.pos.xy).damageamount != 0)
			{
				if (tries < 10)
				{
					if (meta_developer)
						Console.printf("Imp Lord at %i x %i declined to teleport into nukage", self.pos.x, self.pos.y);
					tries++;
					SetStateLabel("TeleLoopAway");
				}
			}
		}
		TNT1 A 0 { bFRIGHTENED = 0; }
		TNT1 A 0 A_Jump(128, "TeleStop");
		loop;
	TeleStop:
		ROAC A 0 { A_PlaySound("monsters/implord/tele"); A_SpawnItem("MetaImpLordTeleportFog"); tries = 0; }
		ROAC A 0 { bFLOAT = 0; bNOGRAVITY = 0; }
		ROAC H 1 A_CheckSolidFooting("TeleLanding");
		wait;
	TeleLanding:
		ROAC A 0 A_JumpIf(meta_mapbreakers == 1, "TeleLandingFull");
		goto TeleLandingHalf;
	TeleLandingFull:
		ROAC A 8
		{
			A_Stop();
			A_PlaySound("bodyfalls/medium");
		}
		ROAC A 0 { bDROPOFF = 1; }
		goto See;
	TeleLandingHalf:
		ROAC A 16 A_Stop;
		ROAC A 0 { bDROPOFF = 1; }
		goto See;
	Pain:
		ROAC H 0;
		ROAC H 2;
		ROAC H 2 A_Pain;
		Goto See;
	Punched:
		ROAC H 3;
		ROAC H 3;
		ROAC H 12 A_Pain;
		Goto See;
	Death:
		ROAC I 0 { bSPRITEFLIP = random(0,1); } // random chance of sprite flipping
		TNT1 A 0 A_SpawnItem("SoulSpawner");
		ROAC I 8 A_GiveToTarget("ScoreItem", 150);
		ROAC J 8 A_Scream;
		ROAC K 6;
		ROAC L 6 A_NoBlocking;
		ROAC M -1 A_PlaySound("bodyfalls/medium");
		Stop;
	XDeath:
		TNT1 A 0 A_SpawnItem("SoulSpawner");
		ROAC N 5 A_GiveToTarget("ScoreItem", 150);
		TNT1 A 0 A_SpawnItemEx("MetaGibber",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS);
		Stop;
	Raise:
		ROAC M 0 { bSPRITEFLIP = 0; }
		ROAC MLKJI 8;
		Goto See;
	}
}

class MetaImpLordTeleportFog : Actor
{
	Default
	{
		+NOINTERACTION
		+NOTELEPORT
		+NOGRAVITY
		RenderStyle "Add";
	}

	States
	{
	Spawn:
		TFOG ABCDE 3 Bright A_SpawnProjectile("MetaImpLordTeleportFogParticle", 8, 0, random(0,360), CMF_AIMDIRECTION, random(-128,128));
		TFOG FGHIJ 3 Bright;
		Stop;
	}
}

class MetaImpLordTeleportFogParticle : Actor
{
	Default
	{
		+NOINTERACTION
		+NOTELEPORT
		+NOGRAVITY
		+MISSILE
		RenderStyle "Add";
		speed 5;
		scale 0.5;
	}

	States
	{
	Spawn:
		TFOG ABCDEFGHIJ 1 Bright;
		Stop;
	}
}