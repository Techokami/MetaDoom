/*
----------------------------
MetaDoom
Actor Definitions
Optional Content
----------------------------
*/

// Wolfenstein 3D - MS-DOS version.
// Basically the same as the Doom 2 monster, but with more aiming angles and
// no hitscan. Maybe twiddle with him a bit in the future?
class MetaWolfensteinSS : MonsterCore
{
	Default
	{
		Health 50;
		Radius 20;
		Height 56;
		Speed 8;
		PainChance 170;
		Monster;
		+FLOORCLIP
		SeeSound "monsters/wolfss/see";
		PainSound "monsters/wolfss/pain";
		DeathSound "monsters/wolfss/die";
		Obituary "$OB_WOLFSS";
		Dropitem "Clip";
		Tag "DOS SS";
		//$Category "Monsters/MetaDoom/Wolfenstein SS"
		//$Title "MS-DOS"
		//$Color 12
	}

	States
	{
	Spawn:
		SSWV AB 10 A_Look;
		Loop;
	See:
		SSWV AABBCCDD 3 A_MetaChase;
		Loop;
	Missile:
		SSWV E 10 A_FaceTarget;
		SSWV F 10 A_FaceTarget;
		SSWV G 4 BRIGHT 
		{
			A_PlaySound ("monsters/wolfss/fire");
			A_SpawnProjectile("MonsterZombiemanTracer",32,0,random(-4,4),CMF_OFFSETPITCH,random(-4,4));
		}
		SSWV F 6 A_FaceTarget;
		SSWV G 4 BRIGHT 
		{
			A_PlaySound ("monsters/wolfss/fire");
			A_SpawnProjectile("MonsterZombiemanTracer",32,0,random(-4,4),CMF_OFFSETPITCH,random(-4,4));
		}
		SSWV F 1 A_CPosRefire;
		Goto Missile+1;
	Pain:
		SSWV H 3;
		SSWV H 3 A_Pain;
		Goto See;
	Punched:
		SSWV H 3;
		SSWV H 12 A_Pain;
		Goto See;
	Death:
		SSWV I 0 { bSPRITEFLIP = random(0,1); } // random chance of sprite flipping
		SSWV I 5 A_GiveToTarget("ScoreItem", 666);
		SSWV J 5 A_Scream;
		SSWV K 5 A_NoBlocking;
		SSWV L 5;
		SSWV M -1 A_PlaySound("monsters/wolfmac/body");
		Stop;
	XDeath:
		SSWV N 5;
		"####" # 0 A_Fall;
		TNT1 A 0 A_SpawnItemEx("MetaGibber",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS);
		Stop;
	Raise:
		SSWV M 5 { bSPRITEFLIP = 0; }
		SSWV LKJI 5;
		Goto See;
	}
}

// Wolfenstein 3D - SNES Version.
// The sprites weren't ACTUALLY this low-res, but with the low render resolution
// it's not like you could tell.
class MetaWolfensteinSNES : MonsterCore
{
	Default
	{
		Health 50;
		Radius 20;
		Height 56;
		Speed 8;
		PainChance 200;
		Monster;
		+FLOORCLIP
		+DOHARMSPECIES
		SeeSound "monsters/wolfsnes/see";
		PainSound "monsters/wolfsnes/pain";
		DeathSound "monsters/wolfsnes/die";
		ActiveSound "monsters/wolfsnes/see";
		Obituary "$OB_WOLFSNES";
		DropItem "Clip";
		scale 2.0;
		Tag "SNES Guard";
		//$Category "Monsters/MetaDoom/Wolfenstein SS"
		//$Title "SNES"
		//$Color 12
	}

	States
	{
	Spawn:
		WSNS E 10 A_Look;
		Loop;
	See:
		WSNS AABBCCDD 4 A_MetaChase;
		Loop;
	Missile:
		WSNS EF 5 A_FaceTarget;
		WSNS G 0 A_PlaySound ("monsters/wolfsnes/fire");
		WSNS G 8 A_SpawnProjectile("MonsterZombiemanTracer");
		WSNS F 8 A_CPosRefire;
		Goto Missile+2;
	Pain:
		WSNS H 3;
		WSNS H 3 A_Pain;
		Goto See;
	Punched:
		WSNS H 3;
		WSNS H 12 A_Pain;
		Goto See;
	Death:
		WSNS H 10 A_GiveToTarget("ScoreItem", 666);
		WSNS I 10 A_Scream;
		WSNS J 10 A_NoBlocking;
		WSNS K -1 A_PlaySound("monsters/wolfmac/body");
		Stop;
	XDeath:
		WSNS I 5 A_GiveToTarget("ScoreItem", 100);
		"####" # 0 A_Fall;
		TNT1 A 0 A_SpawnItemEx("MetaGibber",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS);
		Stop;
	Raise:
		WSNS K 5;
		WSNS JIH 5;
		Goto See;
	}
}

// Escape from Castle Wolfenstein - Apple II version.
// Getting these to look and sound semi-tolerable was a challenge!
class MetaWolfensteinApple : MonsterCore
{
	Default
	{
		Health 50;
		Radius 20;
		Height 56;
		Speed 8;
		PainChance 200;
		Monster;
		+FLOORCLIP
		+DOHARMSPECIES
		SeeSound "monsters/wolfapple/see";
		PainSound "monsters/wolfapple/pain";
		DeathSound "monsters/wolfapple/die";
		ActiveSound "monsters/wolfapple/see";
		Obituary "$OB_WOLFAPPLE";
		DropItem "Clip";
		scale 2.0;
		Tag "IIrooper";
		//$Category "Monsters/MetaDoom/Wolfenstein SS"
		//$Title "Apple II"
		//$Color 12
	}

	States
	{
	Spawn:
		APPL A 10 A_Look;
		Loop;
	See:
		APPL AABBAACC 4 A_MetaChase;
		Loop;
	Missile:
		APPL A 10 A_FaceTarget;
		APPL A 0 A_PlaySound ("monsters/wolfapple/fire");
		APPL A 4 A_SpawnProjectile("MonsterZombiemanTracer");
		APPL A 4 ;
		APPL A 0 A_PlaySound ("monsters/wolfapple/fire");
		APPL A 4 A_SpawnProjectile("MonsterZombiemanTracer");
		APPL A 4 ;
		APPL A 0 A_PlaySound ("monsters/wolfapple/fire");
		APPL A 4 A_SpawnProjectile("MonsterZombiemanTracer");
		APPL A 8 ;
		Goto See;
	Pain:
		APPL A 3;
		APPL A 3 A_Pain;
		Goto See;
	Punched:
		APPL A 3;
		APPL A 12 A_Pain;
		Goto See;
	Death:
		APPL Z 0 A_GiveToTarget("ScoreItem", 666);
		APPL Z 0 A_Scream;
		APPL Z 0 A_NoBlocking;
		APPL Z -1;
		Stop;
	XDeath:
		APPL A 5 A_GiveToTarget("ScoreItem", 100);
		"####" # 0 A_Fall;
		TNT1 A 0 A_SpawnItemEx("MetaGibber",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS);
		Stop;
	Raise:
		APPL A 5;
		Goto See;
	}
}

// Wolfenstein 3D - Mac OS Classic version.
// All high-res and single-angle and ooh la la.
class MetaWolfensteinMacOS : MonsterCore
{
	Default
	{
		Health 50;
		Radius 20;
		Height 56;
		Speed 8;
		PainChance 170;
		Monster;
		+FLOORCLIP
		SeeSound "monsters/wolfmac/see";
		PainSound "monsters/wolfmac/pain";
		DeathSound "monsters/wolfmac/die";
		ActiveSound "monsters/wolfmac/see";
		AttackSound "monsters/wolfmac/fire";
		Obituary "$OB_WOLFMAC";
		Dropitem "Clip";
		scale 0.6;
		Tag "MacintOfficer";
		//$Category "Monsters/MetaDoom/Wolfenstein SS"
		//$Title "Macintosh"
		//$Color 12
	}

	States
	{
	Spawn:
		WMAC A 10 A_Look;
		Loop;
	See:
		WMAC AABBCCDD 3 A_MetaChase;
		Loop;
	Missile:
		WMAC E 5 A_FaceTarget;
		WMAC F 5 A_FaceTarget;
		WMAC G 4 BRIGHT 
		{
			A_PlaySound ("monsters/wolfss/fire");
			A_SpawnProjectile("MonsterZombiemanTracer",32,0,random(-4,4),CMF_OFFSETPITCH,random(-4,4));
		}
		WMAC F 5 A_FaceTarget;
		WMAC G 4 BRIGHT 
		{
			A_PlaySound ("monsters/wolfss/fire");
			A_SpawnProjectile("MonsterZombiemanTracer",32,0,random(-4,4),CMF_OFFSETPITCH,random(-4,4));
		}
		WMAC F 4 A_CPosRefire;
		Goto Missile+2;
	Pain:
		WMAC H 3;
		WMAC H 3 A_Pain;
		Goto See;
	Punched:
		WMAC H 3;
		WMAC H 12 A_Pain;
		Goto See;
	Death:
		WMAC H 0 { bSPRITEFLIP = random(0,1); } // random chance of sprite flipping
		WMAC H 5 A_GiveToTarget("ScoreItem", 666);
		WMAC I 5 A_Scream;
		WMAC J 5 A_NoBlocking;
		WMAC K -1 A_PlaySound("monsters/wolfmac/body");
		Stop;
	XDeath:
		WMAC H 5;
		"####" # 0 A_Fall;
		TNT1 A 0 A_SpawnItemEx("MetaGibber",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS);
		Stop;
	Raise:
		WMAC K 5 { bSPRITEFLIP = 0; }
		WMAC JIH 5;
		Goto See ;
	}
}

// Wolfenstein RPG - Java Version
// There were back animation frames, but adding those would have required a lot
// of frame duplication, which seems like a good way to drive me mad later.
class MetaWolfensteinJava : MonsterCore
{
	Default
	{
		Health 50;
		Radius 20;
		Height 56;
		Speed 8;
		PainChance 170;
		Monster;
		+FLOORCLIP
		SeeSound "monsters/wolfjava/see";
		PainSound "monsters/wolfjava/pain";
		DeathSound "monsters/wolfjava/die";
		ActiveSound "monsters/wolfjava/act";
		Obituary "$OB_WOLFJAVA";
		Dropitem "Clip";
		scale 0.75;
		Tag "Javanaut";
		//$Category "Monsters/MetaDoom/Wolfenstein SS"
		//$Title "J2ME"
		//$Color 12
	}

	States
	{
	Spawn:
		WJAV A 10 A_Look;
		Loop;
	See:
		WJAV AABBCCDD 3 A_MetaChase;
		Loop;
	Missile:
		WJAV E 10 A_FaceTarget;
		WJAV F 2 BRIGHT 
		{
			A_PlaySound ("monsters/wolfjava/fire");
			A_SpawnProjectile("MonsterZombiemanTracer",32,0,random(-2,2),CMF_OFFSETPITCH,random(-2,2));
		}
		WJAV E 2 A_FaceTarget;
		WJAV F 2 BRIGHT 
		{
			A_PlaySound ("monsters/wolfjava/fire");
			A_SpawnProjectile("MonsterZombiemanTracer",32,0,random(-2,2),CMF_OFFSETPITCH,random(-2,2));
		}
		WJAV E 2 A_FaceTarget;
		WJAV F 2 BRIGHT 
		{
			A_PlaySound ("monsters/wolfjava/fire");
			A_SpawnProjectile("MonsterZombiemanTracer",32,0,random(-2,2),CMF_OFFSETPITCH,random(-2,2));
		}
		WJAV E 2 A_FaceTarget;
		WJAV F 2 BRIGHT 
		{
			A_PlaySound ("monsters/wolfjava/fire");
			A_SpawnProjectile("MonsterZombiemanTracer",32,0,random(-2,2),CMF_OFFSETPITCH,random(-2,2));
		}
		WJAV E 2 A_FaceTarget;
		WJAV F 2 BRIGHT 
		{
			A_PlaySound ("monsters/wolfjava/fire");
			A_SpawnProjectile("MonsterZombiemanTracer",32,0,random(-2,2),CMF_OFFSETPITCH,random(-2,2));
		}
		WJAV E 2 A_FaceTarget;
		WJAV F 2 BRIGHT 
		{
			A_PlaySound ("monsters/wolfjava/fire");
			A_SpawnProjectile("MonsterZombiemanTracer",32,0,random(-2,2),CMF_OFFSETPITCH,random(-2,2));
		}
		WJAV E 10 A_FaceTarget;
		Goto See;
	Melee:
		WJAV A 4;
		WJAV G 8 A_CustomMeleeAttack(15, "monsters/wolfjava/kick", "monsters/revenant/wiff", "KrautBoot");
		WJAV A 4;
		goto See;
	Pain:
		WJAV H 3;
		WJAV H 3 A_Pain;
		Goto See;
	Punched:
		WJAV H 3;
		WJAV H 12 A_Pain;
		Goto See;
	Death:
		WJAV I 0 { bSPRITEFLIP = random(0,1); } // random chance of sprite flipping
		WJAV I 0 A_GiveToTarget("ScoreItem", 666);
		WJAV I 0 A_Scream;
		WJAV I 0 A_NoBlocking;
		WJAV IIIIIIIIIIIIIIIIII 1 A_SpriteOffset(random(-1,1),0);
		WJAV I 10 A_SpriteOffset(0,0);
		WJAV J -1 A_PlaySound("monsters/wolfjava/thud");
		Stop;
	XDeath:
		WJAV I 5;
		"####" # 0 A_Fall;
		TNT1 A 0 A_SpawnItemEx("MetaGibber",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS);
		Stop;
	Raise:
		WJAV I 1 { bSPRITEFLIP = 0; }
		WJAV IIIIIIIII 1 A_SpriteOffset(random(-1,1),0);
		WJAV A 0 A_SpriteOffset(0,0);
		Goto See;
	}
}