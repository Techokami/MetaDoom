/*
----------------------------
MetaDoom
Actor Definitions
Unique Bosses
----------------------------
*/

// The Mother Demon from Doom 64! Art from the Doom 64 for Doom 2 TC by Cage.
// Some code taken from the Doom 64 for Doom 2 TC by Mechadon.
// A good deal of attack code taken from Doom 64 Retribution by Nevander with
// lots of tweaks and edits to taste.
class MetaMotherDemon : MonsterCore
{
	Default
	{
		Health 3500;
		Radius 40;
		Height 105;
		Mass 10000;
		Speed 25;
		PainChance 4;
		Monster;
		MinMissileChance 160;
		+BOSS
		+MISSILEMORE
		+FLOORCLIP
		+NORADIUSDMG
		+DONTMORPH
		SeeSound "monsters/motherdemon/see";
		PainSound "monsters/motherdemon/pain";
		DeathSound "monsters/motherdemon/die";
		ActiveSound "monsters/motherdemon/idle";
		Obituary "$OB_MOTHERDEMON";
		damagefactor "ChaingunTurret", 0.2;
		painchance "ChaingunTurret", 0;
		painchance "Vampire", 128;
		damagefactor "Vampire", 0.25;
		damagefactor "Tesla", 0.25;
		MonsterCore.CanBeScared false;
		Tag "Mother Demon";
		//$Category "Monsters/MetaDoom/Bosses"
		//$Title "Mother Demon"
		//$Color 12
	}
	
	States
	{
		Spawn:
			RECT AB 10 A_Look;
			Loop;
		See:
			RECT U 0;
			RECT U 0 A_Quake(5,30,0,2048,0);
			RECT U 35 A_FaceTarget();
			goto Roam;
		Roam:
			RECT AAABBBCCCDDD 2 A_Chase;
			Loop;
		Missile:
		MissileClassic:
			RECT HI 6 A_FaceTarget;
			RECT J 14 Bright
			{
				A_FaceTarget();
				A_PlaySound("monsters/motherdemon/magic", CHAN_6);
				A_SpawnProjectile("MetaMotherFire", 0, 0, 0, 2, 0);
				A_SpawnProjectile("MetaMotherFire", 0, 0, 90, 2, 0);
				A_SpawnProjectile("MetaMotherFire", 0, 0, -90, 2, 0);
				A_SpawnProjectile("MetaMotherFire", 0, 0, 180, 2, 0);
			}
			RECT IEF 8 A_FaceTarget;
			RECT G 14 Bright
			{
				A_FaceTarget();
				A_PlaySound("monsters/motherdemon/fire", CHAN_7);
				A_SpawnProjectile("MetaMotherBall", 100, -16,  90); // TOP LEFT
				A_SpawnProjectile("MetaMotherBall",  40, -48,  90); // BOTTOM LEFT
				A_SpawnProjectile("MetaMotherBall", 100,  16, -90); // TOP RIGHT
				A_SpawnProjectile("MetaMotherBall", 40,  48, -90); // BOTTOM RIGHT
			}
			Goto Roam;
		Pain.Axe:
		Pain.Fist:
		Pain.BerserkFist:
		Pain:
			RECT K 3;
			RECT K 3 A_Pain;
			Goto Roam;
		Death:
			RECT L 0 { bSPRITEFLIP = random(0,1); }
			RECT L 20 A_Scream;
			RECT M 10;
			RECT N 8
			{
				A_PlaySound("monsters/motherdemon/gib", CHAN_AUTO, 1.0, 0, ATTN_NONE);
				A_Fall();
				for (int i = 0; i < 4; i++)
				{
					A_SpawnProjectile ("CyberdemonGibber", random(8,32), 0, random (0, 360), 2, random (32, 64));
					A_SpawnProjectile ("CyberdemonGibber", random(32,64), 0, random (0, 360), 2, random (64, 110));
				}
			}
			RECT O 8;
			RECT PQRS 6;
			RECT T 48;
			RECT T -1 A_KillMaster; //A_BrainDie;
			Stop;
	}
}

// Attack code ported from Doom 64 Retribution by Nevander and heavily edited.
// Sort of like a Revenant fireball, but it only tracks for a little bit after
// being spawned, which is a bit fairer given everything else going on.
class MetaMotherBall : Actor
{
	Default
	{
		Radius 11;
		Height 8;
		Speed 14;
		Damage 10;
		Projectile;
		+SEEKERMISSILE 
		+RANDOMIZE
		Decal "RevenantScorch";
	}
	
	int trackerizer;
	
	States
	{
		Spawn:
			RBAL A 0 BRIGHT;
			RBAL A 0 A_PlaySound ("weapons/rocketlauncher/loop", CHAN_BODY, 1.0, 1);
			RBAL A 0 { trackerizer = 0; }
		Fly:
			RBAL A 0 BRIGHT A_SpawnProjectile("MetaMotherBallTrailSmoke", 0, 0, 0, 2, 0);
			RBAL A 2 BRIGHT A_SeekerMissile(5, 15, SMF_PRECISE);
			RBAL B 0 BRIGHT A_SpawnProjectile("MetaMotherBallTrailSmoke", 0, 0, 0, 2, 0);
			RBAL B 2 BRIGHT A_SeekerMissile(5, 15, SMF_PRECISE);
			RBAL A 0
			{
				// Only track for a little bit
				trackerizer++;

				if (trackerizer >= 8)
				{
					return ResolveState("StopSeeking");
				} else {
					return ResolveState(null);
				}
			}
			Loop;
		Death:
		XDeath:
			TNT1 A 0;
			TNT1 A 0 A_SpawnItem("MetaRevenantTracerExplosion");
			Stop;
		StopSeeking:
			RBAL A 0 BRIGHT A_SpawnProjectile("MetaMotherBallTrailSmoke", 0, 0, 0, 2, 0);
			RBAL A 2 BRIGHT;
			RBAL B 0 BRIGHT A_SpawnProjectile("MetaMotherBallTrailSmoke", 0, 0, 0, 2, 0);
			RBAL B 2 BRIGHT;
			Loop;
	}
}

class MetaMotherBallTrailSmoke : Actor
{
	Default
	{
		+NOINTERACTION
		+NOGRAVITY
		+DONTSPLASH
		RenderStyle "Translucent";
		Alpha 0.50;
		VSpeed 1;
	}
	
	states
	{
	Spawn:
		PUF3 ABCD 2 A_FadeOut;
	Finalize:
		PUF3 E 2 A_FadeOut;
		loop;
	}
}

class MetaMotherFire : Actor
{
	Default
	{
		Height 8;
		Radius 6;
		Damage 6;
		Speed 15;
		RenderStyle "Translucent";
		Alpha 0.50;
		damagetype "SpiderdemonTKAttack";
		PROJECTILE;
		+RANDOMIZE
		+NODAMAGETHRUST
		+FLOORHUGGER
	}
	
	States
	{
		Spawn:
			FIRE A 0;
			FIRE A 0 A_PlaySound ("monsters/motherdemon/flare", CHAN_BODY, 0.5, 1);
			FIRE ABCDE 4 BRIGHT A_SpawnProjectile("MetaMotherFireTrail", 0, 0, 0, 2, 0);
			Loop;
		Death:
			FIRE E 0 BRIGHT;
			Stop;
	}
}

class MetaMotherFireTrail : Actor
{
	Default
	{
		Height 8;
		Radius 6;
		Speed 0;
		Damage 0;
		RenderStyle "Translucent";
		Alpha 0.60;
		+NOGRAVITY
	}
	
	States
	{
		Spawn:
			FIRE ABCDE 4 BRIGHT A_FadeOut(0.20);
			Loop;
	}
}

////////////////////////////////////////////////////////////////////////////////

// The Hell Guard from Doom 2016! Missing a couple of abilities from that game,
// like the shield, but that's because fighting shield monsters in the Doom
// engine is awful. Blame Hexen!
class MetaHellGuard : MonsterCore
{
	Default
	{
		Health 3000;
		Radius 40;
		Height 105;
		Mass 10000;
		Speed 25;
		Monster;
		MinMissileChance 160;
		MeleeRange 72;
		+BOSS
		+MISSILEMORE
		+FLOORCLIP
		+NORADIUSDMG
		+DONTMORPH
		+NODROPOFF
		+ROLLSPRITE
		+ROLLCENTER
		SeeSound "monsters/hellguard/see";
		PainSound "monsters/hellguard/pain";
		DeathSound "monsters/hellguard/die";
		ActiveSound "monsters/hellguard/idle";
		Obituary "$OB_HELLGUARD";
		damagefactor "ChaingunTurret", 0.2;
		painchance "ChaingunTurret", 0;
		painchance "Vampire", 128;
		damagefactor "Vampire", 0.25;
		damagefactor "Tesla", 0.25;
		MonsterCore.CanBeScared false;
		Tag "Hell Guard";
		//$Category "Monsters/MetaDoom/Bosses"
		//$Title "Hell Guard"
		//$Color 12
		
	}
	
	States
	{
		Spawn:
			HGAR ZZ 10 A_Look;
			Loop;
		See:
			HGAR ] 0;
			HGAR ] 0 A_Quake(5,30,0,2048,0);
			HGAR ] 35 A_FaceTarget();
			goto Roam;
		Roam:
			HGAR A 0 A_PlaySound("monsters/hellguard/step");
			HGAR AABBCC 3 A_Chase("Melee","Missile");
			HGAR A 0 A_PlaySound("monsters/hellguard/step");
			HGAR DDEECC 3 A_Chase("Melee","Missile");
			Loop;
		Melee:
			HGAR KLMN 2 A_FaceTarget;
			HGAR O 16
			{
				A_FaceTarget();
				A_Stop();
				A_PlaySound("monsters/hellknight/land",CHAN_7);
				A_SpawnItem("MetaHellGuardStompDamager",32,0);
				A_SpawnItem("BaronShockwave",32,1);
			}
			HGAR ZZ 8 A_FaceTarget;
			goto Roam;
		Missile:
			HGAR E 0 A_Jump(128, "Roll");
			HGAR E 0 A_Jump(64, "MissileFast", "TauntBefireMissileClassic");
		MissileClassic:
			HGAR E 0 A_SetRoll(0);
			HGAR FG 3 A_FaceTarget;
		MissileClassicLoop:
			HGAR H 6 A_FaceTarget;
			HGAR I 6 Bright
			{
				A_FaceTarget();
				A_PlaySound("monsters/hellguard/fire", CHAN_6);
				A_SpawnProjectile("MetaHellGuardShot",56,0,0);
				A_SpawnProjectile("MetaHellGuardShot",56,0,11.25);
				A_SpawnProjectile("MetaHellGuardShot",56,0,22.5);
				A_SpawnProjectile("MetaHellGuardShot",56,0,-11.25);
				A_SpawnProjectile("MetaHellGuardShot",56,0,-22.5);
			}
			HGAR J 6 Bright A_FaceTarget;
			HGAR H 0 A_MonsterRefire(40, "See");
			HGAR H 3 A_FaceTarget;
			Goto MissileClassicLoop;
		MissileFast:
			HGAR E 0 A_SetRoll(0);
			HGAR FG 3 A_FaceTarget;
		MissileFastLoop:
			HGAR H 2 A_FaceTarget;
			HGAR I 2 Bright
			{
				A_FaceTarget();
				A_PlaySound("monsters/hellguard/fastfire", CHAN_6, 1.0, 0, ATTN_NORM, 0, frandom(0.8,1.2));
				A_SpawnProjectile("MetaHellGuardShot",56,0,random(-10,10));
			}
			HGAR J 2
			{
				A_FaceTarget();
				A_CPosRefire();
			}
			loop;
		TauntBefireMissileClassic:
			HGAR KLMN 2 A_FaceTarget;
			HGAR O 6
			{
				A_FaceTarget();
				A_Stop();
				A_PlaySound("monsters/hellknight/land",CHAN_7);
				A_SpawnItem("MetaHellGuardStompDamager",32,0);
				A_SpawnItem("BaronShockwave",32,1);
			}
			HGAR KLMN 2 A_FaceTarget;
			HGAR O 6
			{
				A_FaceTarget();
				A_Stop();
				A_PlaySound("monsters/hellknight/land",CHAN_7);
				A_SpawnItem("MetaHellGuardStompDamager",32,0);
				A_SpawnItem("BaronShockwave",32,1);
			}
			HGAR Z 8 A_FaceTarget;
			goto MissileClassic;
		Roll:
			HGAR E 0 A_FaceTarget;
			HGAR E 0 A_PlaySound("monsters/hellguard/roll");
			HGAR E 0 A_Jump(64, "RollLeft");
			goto RollRight;
		RollLeft:
			HGAR C 0 ThrustThing(angle*256/360+192, 12, 0, 0);
			HGAR CQRRRRRRRRRRRRRQC 1 A_SetRoll((roll + 20));
			HGAR C 0 A_Jump(64, "MissileFast", "RollRight");
			goto MissileClassic;
		RollRight:
			HGAR C 0 ThrustThing(angle*256/360+64, 12, 0, 0);
			HGAR CQRRRRRRRRRRRRRQC 1 A_SetRoll((roll - 20));
			HGAR C 0 A_Jump(64, "MissileFast", "RollLeft");
			goto MissileClassic;
		Pain.Axe:
		Pain.Fist:
		Pain.BerserkFist:
		Pain:
			HGAR S 3 A_SetRoll(0);
			HGAR S 3 A_Pain;
			Goto Roam;
		Death:
			HGAR T 0 { bSPRITEFLIP = random(0,1); }
			HGAR T 20 A_Scream;
			HGAR U 8
			{
				A_PlaySound("monsters/hellguard/gib", CHAN_AUTO, 1.0, 0, ATTN_NONE);
				A_Fall();
				for (int i = 0; i < 8; i++)
				{
					A_SpawnProjectile ("CyberdemonGibber", random(95,98), 0, random (0, 360), 2, random (32, 64));
				}
			}
			HGAR VUVUV 8;
			HGAR WXY 4;
			HGAR [ 32
			{
				A_PlaySound("bodyfalls/heavy",CHAN_6,1.0,0,ATTN_NONE,false,0.5);
				A_Quake(5,8,0,2048,0);
			}
			HGAR [ -1 A_KillMaster; //A_BrainDie;
			Stop;
	}
}

class MetaHellGuardShot : Actor
{
	Default
	{
		Radius 6;
		Height 8;
		Speed 20;
		Damage 8;
		Projectile;
		+RANDOMIZE
		RenderStyle "Add";
		Alpha 1;
		DeathSound "monsters/mancubus/boom";
		decal "Scorch";
	}

	States
	{
	Spawn:
		HGAP A 0;
		HGAP A 0 A_PlaySound("monsters/imp/fireball/loop", CHAN_BODY, 1.0, 1);
		HGAP AB 4 BRIGHT A_SpawnItem ("MetaBaronBallEffect", 0, 0, 0, 0);
		Loop;
	Death:
		HGAP C 0 A_Quake (3,5,0,750,0);
		HGAP C 8 BRIGHT;
		HGAP D 6 BRIGHT;
		HGAP E 4 BRIGHT;
		Stop;
	}
}

class MetaHellGuardStompDamager : Actor
{
	Default
	{
		+NOBLOCKMAP
		+NOGRAVITY
		Obituary "$OB_HELLGUARD_STOMP";
	}
	
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 0 
		{
			A_Explode(128,48,XF_NOTMISSILE|!XF_HURTSOURCE|XF_NOSPLASH);
			
			BlockThingsIterator it = BlockThingsIterator.Create(self,128,0);
			while (it.Next())
			{
				Actor mo = it.thing;
				
				if (mo is "PlayerPawn"
				|| mo is "Inventory"
				|| (mo is "MonsterCore" && mo.bBOSS == false))
				{
					if (mo.pos.z == mo.floorz)
						mo.vel.z = 8;
					break;
				}
			}
			
		}
		Stop;
	}
}

////////////////////////////////////////////////////////////////////////////////

// The Harbinger of Doom, the Cyberdemon pre-Spearing. Attack is a bit like a
// Super-Baron more than anything. May change in the future to be fairer, but
// plays nice on intended map (DOOM2 MAP32) due to abundance of cover.
class MetaHarbinger : MonsterCore
{
	Default
	{
		Health 4000;
		Radius 40;
		Height 110;
		Mass 1000;
		Speed 16;
		PainChance 20;
		Monster;
		MinMissileChance 180;
		+BOSS 
		+MISSILEMORE
		+FLOORCLIP
		+NORADIUSDMG
		+DONTMORPH
		//+BOSSDEATH
		SeeSound "monsters/harbinger/see";
		PainSound "monsters/harbinger/pain";
		DeathSound "monsters/harbinger/die";
		ActiveSound "monsters/harbinger/laugh";
		Obituary "$OB_HARBINGER";
		damagefactor "ChaingunTurret", 0.2;
		painchance "ChaingunTurret", 0;
		damagefactor "SpiderdemonAttack", 0.5;
		painchance "SpiderdemonAttack", 0;
		damagefactor "SpiderdemonTKAttack", 0.5;
		painchance "Vampire", 128;
		damagefactor "Vampire", 0.25;
		damagefactor "Tesla", 0.25;
		MonsterCore.CanBeScared false;
		Tag "Harbinger of Doom";
		//$Category "Monsters/MetaDoom/Bosses"
		//$Title "Harbinger of Doom"
		//$Color 12
	}
	
	States
	{
	Spawn:
		SUPR AB 10 A_Look;
		Loop;
	See:
		SUPR H 0;
		SUPR H 0 A_Quake(5,30,0,2048,0);
		SUPR H 35 A_FaceTarget();
		goto Roam;
	Roam:
		SUPR A 3 { A_Chase(); A_PlaySound("monsters/cyberdemon/hoof"); }
		SUPR ABB 3 A_Chase();
		SUPR C 3 { A_Chase(); A_PlaySound("monsters/cyberdemon/hoof"); }
		SUPR CDD 3 A_Chase();
		goto Roam;		
	Missile:
		SUPR E 0 A_PlaySound("monsters/harbinger/fire", CHAN_6);
		SUPR EEFF 3 A_FaceTarget();
		SUPR G 12 BRIGHT
		{
			A_FaceTarget();
			//A_PlaySound("monsters/hellguard/fire", CHAN_6);
			A_SpawnProjectile("MetaHarbingerShot",64,0,0);
		}
		Goto Roam;
	// YOUR PUNY MITTS DO NOT WORK HERE
	Pain.Axe:
	Pain.Fist:
	Pain.BerserkFist:
	Pain:
		SUPR H 0;
		SUPR H 10 A_Pain;
		Goto Roam;
	Death:
		SUPR I 0 A_GiveToTarget("ScoreItem", 10000);
		SUPR I 20 A_Scream;
		SUPR J 10;
		SUPR K 8
		{
			A_Quake (4,25,0,750,0);
			A_PlaySound("gibbage/gib", CHAN_AUTO, 1.0, 0, ATTN_NONE, 0, 0.8);
			A_FaceTarget();
			for (int i = 0; i < 4; i++)
			{
				A_SpawnProjectile ("CyberdemonGibber", random(60,64), 42, 0, 2, random (32, 64));
			}
		}
		SUPR K 8;
		SUPR L 8
		{
			A_NoBlocking();
			A_PlaySound("gibbage/gib", CHAN_AUTO, 1.0, 0, ATTN_NONE, 0, 0.8);
			A_FaceTarget();
			for (int i = 0; i < 4; i++)
			{
				A_SpawnProjectile ("CyberdemonGibber", random(32,32), -42, 0, 2, random (32, 64));
			}
		}
		SUPR M 0 A_SetScale(1.0,0.9);
		SUPR MN 10;
		SUPR M 10 A_PlaySound("monsters/harbinger/moan", CHAN_AUTO, 1.0, 0, ATTN_NONE, 0, 1.0);
		SUPR NM 10;
		SUPR N 10 A_PlaySound("monsters/harbinger/moan", CHAN_AUTO, 1.0, 0, ATTN_NONE, 0, 1.5);
		SUPR N 0 A_PlaySound("monsters/harbinger/teleport", CHAN_7, 1.0, 0, ATTN_NONE, 0, 1.0);
		SUPR MMNNMMNN 5
		{
			for (int i = 0; i < 12; i++)
			{
				A_SpawnProjectile ("MetaHarbingerTeleportFog", random(8,56), random(-40,40), random (0, 360), 2, random (-8, 8));
			}
		}
		TNT1 AAAAAAA 2
		{
			for (int i = 0; i < 12; i++)
			{
				A_SpawnProjectile ("MetaHarbingerTeleportFog", random(8,56), random(-40,40), random (0, 360), 2, random (-8, 8));
			}
		}
		TNT1 A 10 A_KillMaster; //A_BossDeath
		Stop;
	}
}

class MetaHarbingerShot : Actor
{
	Default
	{
		Radius 6;
		Height 8;
		Speed 20;
		Damage 8;
		Projectile;
		+RANDOMIZE
		RenderStyle "Add";
		Alpha 1;
		DeathSound "monsters/mancubus/boom";
		decal "Scorch";
	}

	States
	{
	Spawn:
		HGAP A 0;
		HGAP A 0 A_PlaySound("monsters/imp/fireball/loop", CHAN_BODY, 1.0, 1);
		HGAP ABABABAB 4 BRIGHT A_SpawnItem ("MetaBaronBallEffect", 0, 0, 0, 0);
	Death:
		HGAP C 0;
		HGAP C 0 
		{
			A_PlaySound("monsters/bruisers/fireballsplit", CHAN_6);
			A_Quake (2,5,0,750,0);
			A_SpawnProjectile ("MetaSuperBaronBall", 0, 0, 0, CMF_AIMDIRECTION, 0);
			A_SpawnProjectile ("MetaSuperBaronBall", 0, 0, 90, CMF_AIMDIRECTION, 0);
			A_SpawnProjectile ("MetaSuperBaronBall", 0, 0, 180, CMF_AIMDIRECTION, 0);
			A_SpawnProjectile ("MetaSuperBaronBall", 0, 0, 270, CMF_AIMDIRECTION, 0);
			
		}
		HGAP C 8 BRIGHT;
		HGAP D 6 BRIGHT;
		HGAP E 4 BRIGHT;
		Stop;
	}
}

class MetaHarbingerTeleportFog : Actor
{
	default
	{
		+NOINTERACTION
		+NOTELEPORT
		+NOGRAVITY
		+ZDOOMTRANS
		RenderStyle "Add";
	}
	States
	{
	Spawn:
		TFOG ABABCDEFGHIJ 6 Bright;
		Stop;
	}
	
}

////////////////////////////////////////////////////////////////////////////////

// Kronos. Super early, might not be shipped in v666
class MetaKronos : MonsterCore
{
	Default
	{
		Health 4000;
		Radius 40;
		Height 110;
		Mass 1000;
		Speed 16;
		PainChance 20;
		Monster;
		MinMissileChance 180;
		+BOSS 
		+MISSILEMORE
		+FLOORCLIP
		+NORADIUSDMG
		+DONTMORPH
		+FLOATBOB
		//+BOSSDEATH
		SeeSound "monsters/kronos/see";
		PainSound "monsters/kronos/pain";
		DeathSound "monsters/kronos/die";
		//ActiveSound "monsters/harbinger/laugh";
		Obituary "$OB_KRONOS";
		damagefactor "ChaingunTurret", 0.2;
		painchance "ChaingunTurret", 0;
		damagefactor "SpiderdemonAttack", 0.5;
		painchance "SpiderdemonAttack", 0;
		damagefactor "SpiderdemonTKAttack", 0.5;
		painchance "Vampire", 128;
		damagefactor "Vampire", 0.25;
		damagefactor "Tesla", 0.25;
		MonsterCore.CanBeScared false;
		Tag "Kronos";
		//$Category "Monsters/MetaDoom/Bosses"
		//$Title "Kronos"
		//$Color 12
	}
	
	States
	{
	Spawn:
		KRON A 10 A_Look;
		Loop;
	See:
		KRON B 0;
		KRON B 0 A_Quake(5,30,0,2048,0);
		KRON B 35 A_FaceTarget();
		goto Roam;
	Roam:
		KRON AAAAAAAA 3 A_Chase();
		goto Roam;		
	Missile:
		KRON A 0;
		KRON A 0 A_Jump(72,"MissileHomers");
	MissileMotherBall:
		KRON BA 8 A_FaceTarget();
		KRON B 12 BRIGHT
		{
			A_FaceTarget();
			A_PlaySound("monsters/hellguard/fire");
			A_SpawnProjectile("MetaMotherBall", 100, -16,  90); // TOP LEFT
			A_SpawnProjectile("MetaMotherBall", 100,  16, -90); // TOP RIGHT
		}
		KRON A 12;
		Goto Roam;
	MissileHomers:
		KRON A 0 A_PlaySound("monsters/kronos/attack", CHAN_6);
		KRON BA 8 A_FaceTarget();
		KRON B 12 BRIGHT
		{
			A_FaceTarget();
			A_PlaySound("monsters/hellguard/fire");
			A_SpawnProjectile("MetaKronosMissile", 100, -16,  0, CMF_ABSOLUTEPITCH, -90);
		}
		KRON A 20 A_FaceTarget();
		Goto Roam;
	// YOUR PUNY MITTS DO NOT WORK HERE
	Pain.Axe:
	Pain.Fist:
	Pain.BerserkFist:
	Pain:
		KRON B 0;
		KRON B 10 A_Pain;
		Goto Roam;
	Death:
		KRON B 0 A_GiveToTarget("ScoreItem", 10000);
		KRON B 20 A_Scream;
		KRON BBBBB 8
		{
			A_SpawnProjectile ("MetaKronosExplosionParticle", random(32,112), 0, random (0, 360), 2, random (-8, 8));
			A_PlaySound("weapons/explosion", CHAN_WEAPON, 0.5, 0, ATTN_NONE, 0, frandom(0.75, 1.25));
		}
		KRON BBBBBBBBBBB 4 
		{
			A_SpawnProjectile ("MetaKronosExplosionParticle", random(32,112), 0, random (0, 360), 2, random (-8, 8));
			A_PlaySound("weapons/explosion", CHAN_WEAPON, 0.75, 0, ATTN_NONE, 0, frandom(0.75, 1.25));
		}
		KRON B 0 A_PlaySound("monsters/cyberdemon/die2", CHAN_AUTO, 1.0, 0, ATTN_NONE);
		KRON BBBBBBBBBBB 2 
		{
			A_SpawnProjectile ("MetaKronosExplosionParticle", random(32,112), 0, random (0, 360), 2, random (-8, 8));
			A_PlaySound("weapons/explosion", CHAN_WEAPON, 1.0, 0, ATTN_NONE, 0, frandom(0.75, 1.25));
		}
		KRON B 8
		{
			bFLOATBOB = false;
			A_NoBlocking();
			for (int i = 0; i < 6; i++)
			{
				A_SpawnProjectile ("CyberdemonExplosionParticle", random(8,55), 0, random (0, 360), 2, random (-8, 8));
				A_SpawnProjectile ("CyberdemonExplosionParticle", random(55,112), 0, random (0, 360), 2, random (-8, 8));
			}
			
			A_Quake (4,25,0,750,0);
			
			for (int i = 0; i < 4; i++)
			{
				A_SpawnProjectile ("CyberdemonGibber", random(8,55), 0, random (0, 360), 2, random (32, 64));
				A_SpawnProjectile ("CyberdemonGibber", random(55,112), 0, random (0, 360), 2, random (64, 110));
			}
			
			A_SpawnItem("CyberdemonExplosionScorchMark",0,0);
			A_SpawnItem("CyberdemonExplosionShockwave",0,55);
		}
		KRON C 30;
		KRON C -1 A_KillMaster; //A_BossDeath
		Stop;
	}
}

class MetaKronosMissile : Actor
{
	Default
	{
		Radius 11;
		Height 8;
		Speed 8;
		Damage 0;
		Projectile;
		+SEEKERMISSILE 
		+RANDOMIZE
	}
	States
	{
	Spawn:
		KRBA A 0;
		KRBA A 0 A_ChangeVelocity(vel.x*2, vel.y*2, vel.z*2, CVF_REPLACE);
		KRBA ABCDABCDABCD 2 BRIGHT
		{
			A_ChangeVelocity(vel.x/2, vel.y/2, vel.z/2, CVF_REPLACE);
			
			for (int i; i < 6; i++)
			{
				A_SpawnParticle("Purple",SPF_RELATIVE,random(16,32),random(3,8),0,0,0,4,frandom(2.5,-2.5),frandom(2.5,-2.5),0,0,0,-0.4);
			}
		}
	Fly:
		KRBA AB 2 BRIGHT 
		{
			A_SeekerMissile(5, 15, SMF_PRECISE);
			
			for (int i; i < 6; i++)
			{
				A_SpawnParticle("Purple",SPF_RELATIVE,random(16,32),random(3,8),0,0,0,4,frandom(2.5,-2.5),frandom(2.5,-2.5),0,0,0,-0.4);
			}
		}
		KRBA A 0 A_PlaySound("monsters/revenant/missile/beep");
		KRBA CD 2 BRIGHT 
		{
			A_SeekerMissile(5, 15, SMF_PRECISE);
			
			for (int i; i < 6; i++)
			{
				A_SpawnParticle("Purple",SPF_RELATIVE,random(16,32),random(3,8),0,0,0,4,frandom(2.5,-2.5),frandom(2.5,-2.5),0,0,0,-0.4);
			}
		}
		KRBA A 0 A_PlaySound("monsters/revenant/missile/beep");
		Loop;
	Death:
		TNT1 A 0 A_SpawnItemEx("MetaKronosExplosion",0,0,24,0,0,0,0,SXF_TRANSFERPOINTERS);
		Stop;
	}
}

class MetaKronosExplosion : Actor
{
	Default
	{
	Radius 10;
	Height 10;
	Speed 5;
	PROJECTILE;
	+NOCLIP
	Decal "Scorch";
	Damagetype "Explosive";
	}
	States
	{
		Spawn:
		EXPL A 0;
		EXPL A 2 BRIGHT
		{
			A_PlaySound("weapons/explosion", CHAN_WEAPON);
			A_Explode(40,90,!XF_HURTSOURCE);
			A_Quake (3,10,0,750,0);
			A_SpawnItem("Shockwave",0,0);
			
			if (pos.z <= (floorz + 64))
			{
				A_SpawnItem("ExplosionScorchMark",0,0);
			}
		}
		EXPL B 2 BRIGHT; 
		EXPL C 2 BRIGHT; 
		EXPL DEF 2;
		Stop;
	}
}

class MetaKronosExplosionParticle : Actor
{
	Default
	{
		Radius 10;
		Height 10;
		Speed 7;
		PROJECTILE;
		+NOCLIP
		+NOGRAVITY
		Decal "Scorch";
		scale 0.5;
	}
	
	States
	{
		Spawn:
		EXPL A 0;
		EXPL A 2 BRIGHT;
		EXPL B 2 BRIGHT ;
		EXPL C 2 BRIGHT ;
		EXPL DEF 2;
		Stop;
	}
}