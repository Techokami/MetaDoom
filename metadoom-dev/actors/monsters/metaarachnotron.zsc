/*
----------------------------
MetaDoom
Actor Definitions
Arachnotron
----------------------------
*/

// Currently more or less identical to the original Doom monster, behavior-wise.

class MetaArachnotron : MonsterCore
{
	Default
	{
		Health 500;
		Radius 64;
		Height 64;
		Mass 600;
		Speed 12;
		PainChance 128;
		Monster;
		+FLOORCLIP
		//+BOSSDEATH
		SeeSound "monsters/arachnotron/see";
		PainSound "monsters/arachnotron/pain";
		DeathSound "monsters/arachnotron/die1";
		ActiveSound "monsters/arachnotron/idle";
		Obituary "$OB_BABY";
		Species "BabySpider";
		Tag "Arachnotron";
		//$Category "Monsters/MetaDoom/Arachnotron"
		//$Title "Arachnotron"
		//$Color 12
	}

	States
	{
	Spawn:
		BSPI AB 10 A_Look;
		Loop;
	See:
		BSPI A 20;
		BSPI A 3 { A_MetaChase(); A_PlaySound("monsters/arachnotron/walk"); }
		BSPI ABBCC 3 A_MetaChase;
		BSPI D 3 { A_MetaChase(); A_PlaySound("monsters/arachnotron/walk"); }
		BSPI DEEFF 3 A_MetaChase;
		Goto See+1;
	Missile:
		BSPI A 20 BRIGHT A_FaceTarget;
		BSPI G 4 BRIGHT A_SpawnProjectile("MetaArachnotronPlasma");
		BSPI H 4 BRIGHT;
		BSPI H 1 BRIGHT A_SpidRefire;
		Goto Missile+1;
	Pain:
		BSPI I 3;
		BSPI I 3 A_Pain;
		Goto See+2;
	Death:
		BSPI J 0 { bSPRITEFLIP = random(0,1); } // random chance of sprite flipping
		BSPI J 0 A_GiveToTarget("ScoreItem", 600);
		TNT1 A 0 A_SpawnItem("SoulSpawner");
		BSPI J 0 A_PlaySound("monsters/arachnotron/die2");
		BSPI J 20 A_Scream;
		BSPI K 7 A_NoBlocking;
		BSPI L 7;
		BSPI M 7 A_PlaySound("monsters/arachnotron/body");
		BSPI NO 7;
		BSPI P -1 A_KillMaster; //A_BossDeath
		Stop;
    Raise:
		BSPI P 0 { bSPRITEFLIP = 0; }
		BSPI P 5;
		BSPI ONMLKJ 5;
		Goto See+2;
	}
}

class MetaArachnotronPlasma : Actor
{
	Default
	{
		Radius 13;
		Height 8;
		Speed 25;
		Damage 5;
		Projectile;
		+RANDOMIZE
		RenderStyle "Add";
		Alpha 0.75;
		decal "ArachnotronScorch";
		SeeSound "monsters/arachnotron/fire";
	}

	States
	{
	Spawn:
		APLS A 0 A_PlaySound ("weapons/plasmarifle/loop", CHAN_BODY, 1.0, 1);
		APLS AB 5 BRIGHT;
		Loop;
	Death:
		APBX A 0 A_PlaySound("weapons/plasmarifle/hit");
		APBX ABCDE 5 BRIGHT;
		Stop;
	}
}


// The Spiborg, a Doom 64 Arach that uses a charge beam attack. Kind of had to go
// off the reservation a bit here...

class MetaSpiborg : MonsterCore
{
	Default
	{
		Health 500;
		Radius 64;
		Height 64;
		Mass 600;
		Speed 12;
		PainChance 128;
		Monster;
		+FLOORCLIP
		//+BOSSDEATH
		SeeSound "monsters/spiborg/see";
		PainSound "monsters/arachnotron/pain";
		DeathSound "monsters/spiborg/die";
		ActiveSound "monsters/spiborg/idle";
		Obituary "$OB_SPIBORG";
		scale 0.75;
		Species "BabySpider";
		Tag "Spiborg";
		//$Category "Monsters/MetaDoom/Arachnotron"
		//$Title "Spiborg"
		//$Color 12
	}

	States
	{
	Spawn:
		2SPI AB 10 A_Look;
		Loop;
	See:
		2SPI A 10;
		2SPI A 3 { A_MetaChase(); A_PlaySound("monsters/spiborg/walk"); }
		2SPI ABB 3 A_MetaChase;
		2SPI C 3 { A_MetaChase(); A_PlaySound("monsters/spiborg/walk"); }
		2SPI CDD 3 A_MetaChase;
		Goto See+1;
	Missile:
		2SPI AAAA 1 BRIGHT
		{
			A_SetPainThreshold (32);
			A_FaceTarget();
			A_SpawnProjectile("SpiborgChargeupParticle",32,0,random(-32,32),CMF_OFFSETPITCH,random(-16,16));
		}
		2SPI A 0 A_PlaySound("monsters/spiborg/charge", CHAN_WEAPON);
		2SPI BBBBBDDDDD 1 BRIGHT { A_FaceTarget(); A_SpawnProjectile("SpiborgChargeupParticle",32,0,random(-32,32),CMF_OFFSETPITCH,random(-16,16)); }
		2SPI BBBDDDBBBDDD 1 BRIGHT { A_FaceTarget(); A_SpawnProjectile("SpiborgChargeupParticle",32,0,random(-32,32),CMF_OFFSETPITCH,random(-16,16)); }
		2SPI BDBDBD 1 BRIGHT { A_FaceTarget(); A_SpawnProjectile("SpiborgChargeupParticle",32,0,random(-32,32),CMF_OFFSETPITCH,random(-16,16)); }
		2SPI E 4 BRIGHT
		{
			A_PlaySound("monsters/spiborg/fire", CHAN_WEAPON);
			A_SpawnProjectile("SpiborgPlasma",32);
			A_SetPainThreshold (0);
		}
		2SPI E 4 BRIGHT;
		2SPI E 1 A_SpidRefire;
		goto See;
	Pain:
		2SPI F 3;
		2SPI F 3 { A_Pain(); A_SetPainThreshold (0); }
		Goto See+2;
	Death:
		2SPI G 0 A_GiveToTarget("ScoreItem", 600);
		TNT1 A 0 A_SpawnItem("SoulSpawner");
		2SPI G 0 A_PlaySound("monsters/arachnotron/die2");
		2SPI G 20 A_Scream;
		2SPI G 0 { A_PlaySound("monsters/spiborg/brainpop", CHAN_7); A_SpawnItem("SpiborgBrainPop",0,56); }
		2SPI H 5 A_NoBlocking;
		2SPI I 5;
		2SPI J 5;
		2SPI K 5 A_PlaySound("monsters/arachnotron/body");
		2SPI L -1 A_KillMaster; //A_BossDeath
		Stop;
    Raise:
		2SPI P 5;
		2SPI LKJIHG 5;
		Goto See+2;
	}
}

class SpiborgPlasma : Actor
{
	Default
	{
		Radius 13;
		Height 8;
		Speed 45;
		Damage 4;
		Projectile;
		+RANDOMIZE
		RenderStyle "Add";
		Alpha 0.75;
		Decal "PlasmaScorch";
	}

	States
	{
	Spawn:
		PLSS A 0 A_PlaySound ("weapons/plasmarifle/loop", CHAN_BODY, 1.0, 1);
		PLSE BC 2 Bright A_SpawnItem("MetaPlasmaPopTrail");
		Loop;
	Death:
		PLSE A 0 A_PlaySound("inventory/teslarocket/explode", CHAN_WEAPON);
		PLSE A 0 A_Quake (1,3,0,750,0);
		PLSE A 0 A_Explode(24,128,0);
		PLSE A 0 A_SpawnItem("ShockwaveSmall");
		PLSE B 0 bright;
		PLSE B 4 BRIGHT;
		PLSE C 3 BRIGHT;
		PLSE D 2 BRIGHT;
		Stop;
	}
}

class SpiborgBrainPop : Actor
{
	Default
	{
		scale 0.25;
		+NOINTERACTION
	}

	States
	{
	Spawn:
		MISL A 0;
		MISL B 0 
		{
			//A_Quake (3,5,0,750,0);
			for (int i = 0; i < 5; i++)
			{
				A_SpawnProjectile ("FlyingBlood", random(8,32), 0, random (0, 360), 2, random (0, 160));
				A_SpawnProjectile ("MetaSploosh", random(8,32), 0, random (0, 360), 2, random (0, 160));
			}
			A_SpawnItem("BloodSplat");
		}
		MISL B 4 BRIGHT;
		MISL C 3 BRIGHT;
		MISL D 2 BRIGHT;
		Stop;
	}
}

class SpiborgChargeupParticle : Actor
{
	Default
	{
		Radius 3;
		Height 3;
		Speed 8;
		FastSpeed 8;
		Damage 0;
		Projectile;
		+RANDOMIZE
		+NOINTERACTION
		RenderStyle "Add";
		Alpha 1;
		scale 0.1;
	}

	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 8;
		TNT1 A 0 a_scalevelocity (-2); //A_ChangeVelocity(-4, 0, 0, CVF_RELATIVE | CVF_REPLACE);
	FlyForABit:
		SBSH AAAAA 1 BRIGHT A_SetScale(scale.x + 0.1);
	Death:
		TNT1 A 0;
		Stop;
	}
}

// The Titantula. The Doom Eternal Arachnotron. Changes attack type when below half health.
class MetaTitantula : MonsterCore
{
	Default
	{
		Health 700;
		Radius 64;
		Height 64;
		Mass 600;
		Speed 12;
		PainChance 128;
		Monster;
		+FLOORCLIP
		+BUDDHA
		//+BOSSDEATH
		SeeSound "monsters/titantula/see";
		PainSound "monsters/titantula/pain";
		DeathSound "monsters/titantula/die";
		ActiveSound "monsters/titantula/roam";
		Obituary "$OB_TITANTULA";
		Species "BabySpider";
		Tag "Titantula";
		Bloodtype "Blueblood";
		BloodColor "0 0 80";
		Decal "bluebloodsplat";
		//$Category "Monsters/MetaDoom/Arachnotron"
		//$Title "Titantula"
		//$Color 12
	}
	
	bool degunned;

	States
	{
	Spawn:
		SCP1 AB 10 A_Look;
		Loop;
	See:
		"####" D 20;
		"####" AA 3 A_MetaChase;
		"####" B 3 { A_MetaChase(); A_PlaySound("monsters/titantula/step"); }
		"####" BCC 3 A_MetaChase;
		"####" B 3 { A_MetaChase(); A_PlaySound("monsters/titantula/step"); }
		"####" B 3 A_MetaChase;
		Goto See+1;
	Missile:
		"####" A 0
		{
			if (degunned)
			{
				SetStateLabel("MissileDegunned");
			}
		}
		SCP1 D 20 BRIGHT A_FaceTarget;
		SCP1 E 2 BRIGHT //A_SpawnProjectile("MetaTitantulaPlasma");
		{
			A_FaceTarget();
			
			if ((ceilingz - floorz) > 76)
			{
				A_SpawnProjectile("MetaTitantulaPlasma",66);
			} else {
				A_SpawnProjectile("MetaTitantulaPlasma");
			}
		}
		SCP1 E 2 BRIGHT A_FaceTarget;
		SCP1 D 1 BRIGHT A_SpidRefire;
		Goto Missile+2;
	MissileDegunned:
		SCP2 D 20 A_FaceTarget;
		SCP2 E 8 BRIGHT { A_FaceTarget(); A_StartSound("monsters/titantula/grenade"); A_SpawnProjectile("MetaTitantulaGrenade"); }
		SCP2 D 10 A_FaceTarget;
		SCP2 E 8 BRIGHT { A_FaceTarget(); A_StartSound("monsters/titantula/grenade"); A_SpawnProjectile("MetaTitantulaGrenade"); }
		SCP2 D 10 A_FaceTarget;
		Goto See;
	Pain:
		"####" F 0 A_JumpIf(degunned == false && health <= 200, "Degunned");
		"####" F 3;
		"####" F 3 A_Pain;
		Goto See+1;
	Death:
		SCP2 G 0 { bSPRITEFLIP = random(0,1); } // random chance of sprite flipping
		SCP2 G 0 A_GiveToTarget("ScoreItem", 600);
		TNT1 A 0 A_SpawnItem("SoulSpawner");
		SCP2 G 0 A_PlaySound("monsters/arachnotron/die2");
		SCP2 G 20 A_Scream;
		SCP2 H 7 A_NoBlocking;
		SCP2 I 7;
		SCP2 J 7 A_PlaySound("monsters/arachnotron/body");
		SCP2 K 7;
		SCP2 L -1 A_KillMaster; //A_BossDeath
		Stop;
    Raise:
		SCP1 K 0 { bSPRITEFLIP = 0; }
		SCP1 K 5;
		SCP1 JIHGA 5;
		SCP1 A 0
		{
			degunned = false;
			bBUDDHA = true;
		}
		Goto See+1;
	Degunned:
		SCP2 Z 0
		{
			A_StopSound(CHAN_BODY);
			A_StartSound("monsters/titantula/weakspot",CHAN_6,0,1.0,ATTN_NORM,1.0);
			if (target is "PlayerPawn")
			{
				target.A_StartSound("monsters/titantula/weakspot",333,CHANF_UI|CHANF_LOCAL,1.0,ATTN_NONE,1.0);
				target.A_StartSound("monsters/titantula/weakspot",334,CHANF_UI|CHANF_LOCAL,1.0,ATTN_NONE,1.0);
			}
			
			health = 200;
			degunned = true;
			bBUDDHA = false;
			bINVULNERABLE = true;
			A_SpawnItemEx("TitantulaGun",-32,0,16,-16,0,16);
			for (int i = 0; i < 3; i++)
			{
				A_SpawnItem("MetaGibItemBox",-32,random(16,42));
				A_SpawnItem("MetaGibItemBox",-32,random(0,16));
			}
		}
		SCP2 Z 24;
		SCP2 Z 0
		{
			bINVULNERABLE = false;
			A_FaceTarget();
		}
		goto See+1;
	}
}

class TitantulaGun : Actor
{
	Default
	{
	+DOOMBOUNCE
	PROJECTILE;
	-NOGRAVITY
	//-NOBLOCKMAP
	-ACTIVATEIMPACT
	+NOTELEPORT
	-TELESTOMP
	-SOLID
	+ROLLSPRITE
	+ROLLCENTER
	+SKYEXPLODE
	Radius 2;
	Damage 0;
	bouncefactor 0.5;
	SeeSound "player/helmetgib";
	gravity 0.5;
	Speed 5;
	scale 0.75;
	}
	
	States
	{
	Spawn:
		SCP0 A 0;
		goto Setup;
	Setup:
		"####" # 0 ThrustThingZ (0, random(32,45), 0, 1);
	ChangeDir:
		"####" # 0 A_Jump(128,"Alt");
		goto See;
	See:
		"####" # 0 A_SpawnItem("FlyingBloodCheap");
		"####" # 0 ThrustThingZ(0,6,1,1);
		"####" # 1 A_SetRoll((roll + 5));
		loop;
	Alt:
		"####" # 0 A_SpawnItem("FlyingBloodCheap");
		"####" # 0 ThrustThingZ(0,6,1,1);
		"####" # 1 A_SetRoll((roll - 5));
		loop;
	Bounce.Floor:
		goto ChangeDir;
	Bounce.Wall:
		goto ChangeDir;
	Death:
		"####" "#" 1 A_FadeOut(0.02);
		Wait;
	}
	
	// Ignore player and enemy collisions
	override bool CanCollideWith(Actor other, bool passive)
	{
		return false;
	}
}

class MetaTitantulaPlasma : Actor
{
	Default
	{
		Radius 13;
		Height 8;
		Speed 25;
		Damage 5;
		Projectile;
		+RANDOMIZE
		RenderStyle "Add";
		Alpha 0.75;
		decal "ArachnotronScorch";
		SeeSound "monsters/titantula/fire";
	}

	States
	{
	Spawn:
		SCLS A 0 A_PlaySound ("weapons/plasmarifle/loop", CHAN_BODY, 1.0, 1);
		SCLS AB 5 BRIGHT;
		Loop;
	Death:
		SCLS A 0 A_PlaySound("weapons/plasmarifle/hit");
		SCBX BCDE 5 BRIGHT;
		Stop;
	}
}

class MetaTitantulaGrenade : Actor
{
	Default
	{
		+CANBOUNCEWATER
		Radius 8;
		Height 8;
		Speed 20;
		Damage 0;
		PROJECTILE;
		+RANDOMIZE
		-NOGRAVITY
		+USEBOUNCESTATE
		+ROLLSPRITE
		BounceType "Doom";
		BounceSound "weapons/grenadelauncher/bounce";
		Obituary "$OB_GRENLAUNCHER";
		scale 0.5;
		ReactionTime 60;	//Just over five seconds;
		Damagetype "Explosive";
	}
	
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 0 ThrustThingZ(0,24,0,1);
		goto Flying;
	Flying:
		SCGR ABCD 4 A_CountDown;
		loop;
	Bounce:
		"####" # 0 A_Jump(128, "Bounce2", "Flying");
		goto Bounce1;
	Bounce1:
		"####" # 0 A_CountDown;
		"####" # 1 A_SetRoll((roll + 5));
		"####" # 1 A_SetRoll((roll + 5));
		"####" # 1 A_SetRoll((roll + 5));
		loop;
	Bounce2:
		"####" # 0 A_CountDown;
		"####" # 1 A_SetRoll((roll - 5));
		"####" # 1 A_SetRoll((roll - 5));
		"####" # 1 A_SetRoll((roll - 5));
		loop;
	Death:
		TNT1 A 0 A_SpawnItemEx("GrenadeLauncherExplosion",0,0,24,0,0,0,0,SXF_TRANSFERPOINTERS);
		Stop;
	}
}

class MetaTitantulaGrenadeExplosion : GrenadeLauncherExplosion
{
	States
	{
	Spawn:
	EXPL A 0;
	EXPL A 2 BRIGHT
	{
		A_PlaySound("weapons/grenadelauncher/explode", CHAN_WEAPON);
		A_Explode(32);
		A_Quake (3,10,0,750,0);
		A_SpawnItem("Shockwave",0,0);
		
		if (pos.z <= (floorz + 64))
		{
			A_SpawnItem("ExplosionScorchMark",0,0);
		}
	}
	EXPL B 2 BRIGHT;
	EXPL C 2 BRIGHT;
	EXPL DEF 2;
	Stop;
	}
}