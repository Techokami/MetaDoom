/*
----------------------------
MetaDoom
Actor Definitions
Archvile Minions
----------------------------
*/

// The Summoner's servants. Useless slow Quake-esque zombies, but they might
// well overwhem the player on a bad day.
class MetaUnwilling : MonsterCore
{
	Default
	{
		Health 20;
		Radius 20;
		Height 56;
		Mass 100;
		Speed 4;
		PainChance 200;
		Monster;
		+FLOORCLIP
		-COUNTKILL
		SeeSound "monsters/unwilling/see";
		PainSound "monsters/unwilling/pain";
		DeathSound "monsters/unwilling/die";
		ActiveSound "monsters/unwilling/idle";
		HitObituary "$OB_SUMMONER_POPCORN";
		Obituary "$OB_SUMMONER_POPCORN";
		damagefactor "HolyWater", 0.25;
		painchance "HolyWater", 255;
		damagefactor "Axe", 50;
		//$Category "Monsters/MetaDoom/Arch-Vile"
		//$Title "Unwilling (Summoner Popcorn)"
		//$Color 12
	}

	States
	{
	Spawn:
		UNWI AB 10 A_Look;
		Loop;
	See:
		UNWI AABBCCDD 3 A_Chase;
		Loop;
	Melee:
	Missile:
		UNWI E 0 A_PlaySound("monsters/unwilling/attack", CHAN_VOICE);
		UNWI EF 4 A_FaceTarget;
		UNWI G 6 A_CustomComboAttack("UnwillingGibBall", 32, 3 * random(1, 8), "monsters/unwilling/punch");
		Goto See;
	Pain:
		UNWI H 2;
		UNWI H 2 A_Pain;
		Goto See;
	Punched:
		UNWI G 3;
		UNWI G 12 A_Pain;
		Goto See;
	Death:
		UNWI I 0 { bSPRITEFLIP = random(0,1); } // random chance of sprite flipping
		UNWI I 6 A_GiveToTarget("ScoreItem", 50);
		UNWI J 6 A_Scream;
		UNWI K 5;
		UNWI L 5 A_NoBlocking;
		UNWI M -1 A_PlaySound("bodyfalls/medium");
		Stop;
	XDeath:
		UNWI J 5 A_GiveToTarget("ScoreItem", 50);
		TNT1 A 0 A_SpawnItemEx("MetaUnwillingGibber",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS);
		Stop;
	Raise:
		UNWI M 0 { bSPRITEFLIP = 0; }
		UNWI MLKJI 8;
		Goto See;
	}
}

class UnwillingGibBall : Actor
{
	Default
	{
		Radius 4;
		Height 9;
		Health 40;
		DamageFunction (random(1,4));
		Speed 35;
		Gravity 0.5;
		Mass 0;
		PROJECTILE;
		+CANBOUNCEWATER
		+NOGRAVITY
		ReactionTime 120;
		Seesound "monsters/unwilling/fire";
		+NOBLOCKMAP
		+MISSILE
		decal "BloodSplat1";
	}

	States
	{
	Spawn:
		ZGIB AAAAA 1 A_SpawnItemEx("UnwillingGibTrail",0,0,0);
		ZGIB A 0 { bNOGRAVITY = 0; }
		Loop;
	Death:
		TNT1 A 1 A_PlaySound ("monsters/unwilling/gib");
		TNT1 A 0
		{
			if (pos.z <= (floorz + 32))
			{
				A_SpawnItem("BloodSplatSmall");
			}
		}
		Stop;
	Crash:
		TNT1 A 1 A_PlaySound ("monsters/unwilling/gib");
		Stop;
	}
}

class UnwillingGibTrail : Actor
{
	Default
	{
		Health 3;
		+NOINTERACTION
		+NOTELEPORT
		+NOGRAVITY
		Scale 0.8;
		RenderStyle "Translucent";
		Alpha 0.8;
	}

	States
	{
	Spawn:
		ZBLD ABCD 3;
		Stop;
	}
}

// Alternate version of MetaGibber to be less intense specifically for the Unwilling.
class MetaUnwillingGibber : Actor
{
	Default
	{
		radius 20;
		height 56;
		+NOCLIP
	}

	states
	{
	Spawn:
	    TNT1 A 0;
	    TNT1 A 0 A_PlaySound("gibbage/gib");
		TNT1 A 0
		{
			for (int i = 0; i < 5; i++)
			{
				A_SpawnProjectile ("FlyingBlood", random(8,32), 0, random (0, 360), 2|CMF_BADPITCH, random (0, 160));
				A_SpawnProjectile ("MetaSploosh", random(8,32), 0, random (0, 360), 2|CMF_BADPITCH, random (0, 160));
			}
			A_SpawnItem("BloodSplat");
		}
	    TNT1 A 1 A_SpawnItemEx ("MetaSploosh", 1, 1, random(8,32), random(0,5), random(0,5), random(8,14), random(0,360), 128, 0);
		TNT1 A 1 A_SpawnItemEx ("MetaSploosh", 1, 1, random(8,32), random(0,5), random(0,5), random(8,14), random(0,360), 128, 0);
		TNT1 A 1 A_SpawnItemEx ("MetaSploosh", 1, 1, random(8,32), random(0,5), random(0,5), random(8,14), random(0,360), 128, 0);
		TNT1 A 1 A_SpawnItemEx ("MetaSploosh", 1, 1, random(8,32), random(0,5), random(0,5), random(8,14), random(0,360), 128, 0);
		TNT1 A 1 A_SpawnItemEx ("MetaSploosh", 1, 1, random(8,32), random(0,5), random(0,5), random(8,14), random(0,360), 128, 0);
		TNT1 A 1 A_SpawnItemEx ("MetaSploosh", 1, 1, random(8,32), random(0,5), random(0,5), random(8,14), random(0,360), 128, 0);
		TNT1 A 1 A_SpawnItemEx ("MetaSploosh", 1, 1, random(8,32), random(0,5), random(0,5), random(8,14), random(0,360), 128, 0);
		TNT1 A 1 A_SpawnItemEx ("MetaSploosh", 1, 1, random(8,32), random(0,5), random(0,5), random(8,14), random(0,360), 128, 0);
		TNT1 A 1 A_SpawnItemEx ("MetaSploosh", 1, 1, random(8,32), random(0,5), random(0,5), random(8,14), random(0,360), 128, 0);
		TNT1 A 1 A_SpawnItemEx ("MetaSploosh", 1, 1, random(8,32), random(0,5), random(0,5), random(8,14), random(0,360), 128, 0);
	    Stop;
	}
}

// Oh hey, it's these nerds.
class MetaDemonicTrooper : MonsterCore
{
	Default
	{
		Health 30;
		Radius 16;
		Height 56;
		Mass 100;
		Speed 8;
		Painchance 160;
		MONSTER;
		-COUNTKILL
		Translation 2;
		Damage 100;
		DeathSound "monsters/demonictrooper/die";
		PainSound "monsters/demonictrooper/pain";
		ActiveSound "monsters/demonictrooper/roam";
		Obituary "$OB_DEMONICTROOPER";
		MonsterCore.CanBeScared false;
		Tag "Demonic Trooper";
	}

	States
	{
	Spawn:
		OPLA A 4 A_Look;
		Loop;
	See:
		OPLA AABBCCDD 2 A_Chase;
		Loop;
	Missile:
		OPLA E 12 A_FaceTarget;
		OPLA F 4 BRIGHT 
		{
			A_SpawnProjectile("MetaDemonicTrooperPistolTracer");
			A_PlaySound("monsters/demonictrooper/fire", CHAN_WEAPON);
		}
		OPLA E 4 A_FaceTarget;
		OPLA F 4 BRIGHT 
		{
			A_SpawnProjectile("MetaDemonicTrooperPistolTracer");
			A_PlaySound("monsters/demonictrooper/fire", CHAN_WEAPON);
		}
		OPLA E 12 A_FaceTarget;
		OPLA F 4 BRIGHT 
		{
			A_SpawnProjectile("MetaDemonicTrooperPistolTracer");
			A_PlaySound("monsters/demonictrooper/fire", CHAN_WEAPON);
		}
		Goto See;
	Pain:
		OPLA G 4;
		OPLA G 4 A_Pain;
		Goto See;
	Death:
	XDeath:
		OPLA O 5 
		{
			A_GiveToTarget("ScoreItem", 50);
			A_Scream();
		}
		TNT1 A 0 A_SpawnItemEx("MetaUnwillingGibber",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS);
		Stop;
	}
}

class MetaDemonicTrooperPistolTracer : FastProjectile
{
	Default
	{
		Alpha 0.90;
		RenderStyle "Add";
		Radius 2;
		Height 2;
		damage 5;
		Speed 75;
		PROJECTILE;
		+BLOODSPLATTER
		+NOEXTREMEDEATH
		Scale 0.4;
		MissileType "MetaDemonicTrooperPistolTracerTrail";
		MissileHeight 8;
		Decal "BulletChip";
		translation "192:207=168:191";
	}
	
	States
	{
	Spawn:
		PISP A 1 BRIGHT;
		Loop;
	Death:
	Crash:
		PUFF A 0 A_PlaySound("weapons/pistol/hitwall", CHAN_BODY, frandom(0.5,1.0));
		PUFF A 2 BRIGHT A_SpawnDebris("MetaDemonicTrooperPistolTracerSpark");
		PUFF B 2 BRIGHT A_SetTranslucent(0.9,0);
		PUFF CD 2 BRIGHT;
		Stop;
	XDeath:
		TNT1 A 3 A_PlaySound("weapons/pistol/hit");
		Stop;
	}
}

class MetaDemonicTrooperPistolTracerTrail : Actor
{ 
	Default
	{
		Alpha 0.75;
		RenderStyle "Add";
		Speed 0;
		+NOINTERACTION
		+NOGRAVITY
		+NOTELEPORT
		+CANNOTPUSH
		+NODAMAGETHRUST
		+DONTSPLASH
		Scale 0.4;
		translation "192:207=168:191";
	}
	States
	{
	Spawn:
		PISP A 1 BRIGHT A_FadeOut(0.3);
		Loop;
	}
}

class MetaDemonicTrooperPistolTracerSpark : Actor
{
	Default
	{
		Health 4;
		radius 3;
		height 6;
		speed .1;
		RenderStyle "Add";
		Alpha 1;
		Scale 0.5;
		Mass 0;
		+missile
		+doombounce
		+FLOORCLIP
		+DONTSPLASH
		+NOTELEPORT
		translation "192:207=168:191";
	}
	States
	{
		PISP A 1;
		PISP AAAAA 1  Bright A_SetTranslucent(.8,1);
		PISP AAAAAA 1  Bright A_SetTranslucent(.6,1);
		PISP AAAAAAAA 1  Bright A_SetTranslucent(.4,1);
		PISP AAAAAAAAAA 1 Bright A_SetTranslucent(.2,1);
		Stop;
	}
	
	override bool CanCollideWith(Actor other, bool passive)
	{
		return false;
	}
}