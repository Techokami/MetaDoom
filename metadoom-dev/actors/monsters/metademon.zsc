/*
----------------------------
MetaDoom
Actor Definitions
Pinky Demon
----------------------------
*/

// Currently more or less identical to the original Doom monster, behavior-wise.
// TODO: Twiddle with, add at least one variant!

class MetaDemon : MonsterCore
{
	Default
	{
		Health 150;
		PainChance 180;
		Speed 10;
		Radius 30;
		Height 56;
		Mass 400;
		Monster;
		+FLOORCLIP
		SeeSound "monsters/pinky/see";
		AttackSound "monsters/pinky/attack";
		PainSound "monsters/pinky/pain";
		DeathSound "monsters/pinky/die";
		ActiveSound "monsters/pinky/idle";
		Obituary "$OB_DEMONHIT";
		painchance "HolyWater", 255;
		Tag "Pinky";
		//$Category "Monsters/MetaDoom/Pinky"
		//$Title "Pinky"
		//$Color 12
	}

	States
	{
	Spawn:
		SARG AB 10 A_Look;
		Loop;
	See:
		SARG AABBCCDD 2 Fast A_MetaChase;
		Loop;
	Melee:
		SARG EF 8 Fast A_FaceTarget;
		SARG G 8 Fast A_SargAttack;
		Goto See;
	Pain:
		SARG H 0;
		SARG H 2 Fast;
		SARG H 2 Fast A_Pain;
		Goto See;
	Punched:
		SARG H 3;
		SARG H 12 A_Pain;
		Goto See;
	Death:
		SARG I 0 { bSPRITEFLIP = random(0,1); } // random chance of sprite flipping
		TNT1 A 0 A_SpawnItem("SoulSpawner");
		SARG I 8 A_GiveToTarget("ScoreItem", 200);
		SARG J 8 A_Scream;
		SARG K 4;
		SARG L 4 A_NoBlocking;
		SARG M 4 A_PlaySound("bodyfalls/heavy");
		SARG N -1;
		Stop;
	XDeath:
		TNT1 A 0 A_SpawnItem("SoulSpawner");
		SARG I 5 A_GiveToTarget("ScoreItem", 200);
		TNT1 A 0 A_SpawnItemEx("MetaGibber",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS);
		Stop;
	Raise:
		SARG N 5 { bSPRITEFLIP = 0; }
		SARG MLKJI 5;
		Goto See;
	}
}

// Doom 3's take on the monster. Has a charge attack and doesn't flinch as easily.
// Some code for charging from D4T project. Thanks DBT and crew!

class MetaBullDemon : MonsterCore
{
	Default
	{
		Health 200;
		PainChance 180;
		Speed 7;
		Radius 28;
		Height 50;
		Mass 400;
		Monster;
		+FLOORCLIP
		SeeSound "monsters/bulldemon/see";
		AttackSound "monsters/bulldemon/bite";
		PainSound "monsters/bulldemon/pain";
		DeathSound "monsters/bulldemon/die";
		//ActiveSound "monsters/pinky/idle";
		Obituary "$OB_BULLDEMONHIT";
		painchance 64;
		painchance "HolyWater", 255;
		Tag "Bull Demon";
		//$Category "Monsters/MetaDoom/Pinky"
		//$Title "Bull Demon"
		//$Color 12
	}

	int user_accelerator;
	int user_chargecooldown;

	States
	{
	Spawn:
		BULD Z 0;
		BULD Z 0 { user_accelerator = 0; user_chargecooldown = 0; }
		BULD Z 10 A_Look;
		Loop;
	See:
		BULD A 0 A_JumpIf(user_chargecooldown < 1 ,"RegularChase");
	ChaseNoCharge:
		BULD A 0 A_PlaySound("monsters/bulldemon/step");
		BULD AA 1 A_MetaChase("Melee", null);
		BULD AA 1 A_MetaChase("Melee", null, CHF_DONTTURN);
		BULD BB 1 A_MetaChase("Melee", null);
		BULD BB 1 A_MetaChase("Melee", null, CHF_DONTTURN);
		BULD A 0 A_PlaySound("monsters/bulldemon/step");
		BULD CC 1 A_MetaChase("Melee", null);
		BULD CC 1 A_MetaChase("Melee", null, CHF_DONTTURN);
		BULD DD 1 A_MetaChase("Melee", null);
		BULD DD 1 A_MetaChase("Melee", null, CHF_DONTTURN);
		D4DW A 0 { user_chargecooldown--; }
		goto See;
	RegularChase:
		BULD A 0 A_PlaySound("monsters/bulldemon/step");
		BULD AA 1 A_MetaChase("Melee", "Missile");
		BULD AA 1 A_MetaChase("Melee", "Missile", CHF_DONTTURN);
		BULD BB 1 A_MetaChase("Melee", "Missile");
		BULD BB 1 A_MetaChase("Melee", "Missile", CHF_DONTTURN);
		BULD A 0 A_PlaySound("monsters/bulldemon/step");
		BULD CC 1 A_MetaChase("Melee", "Missile");
		BULD CC 1 A_MetaChase("Melee", "Missile", CHF_DONTTURN);
		BULD DD 1 A_MetaChase("Melee", "Missile");
		BULD DD 1 A_MetaChase("Melee", "Missile", CHF_DONTTURN);
		D4DW A 0 { user_chargecooldown--; }
		goto See;
	Melee:
		BULD E 4 Fast A_FaceTarget;
		BULD F 4 Fast A_SargAttack;
		BULD GE 4 Fast A_FaceTarget;
		Goto See;
		
	Missile:
		BULD A 0 A_FaceTarget;
		BULD A 0
		{
			FLineTraceData SurfaceData;
			self.LineTrace(self.angle, 72, 0, TRF_THRUACTORS, offsetz: 8, offsetside: 0, data:SurfaceData);
			self.LineTrace(self.angle, 72, 0, TRF_THRUACTORS, offsetz: 8, offsetside: -16, data:SurfaceData);
			self.LineTrace(self.angle, 72, 0, TRF_THRUACTORS, offsetz: 8, offsetside: 16, data:SurfaceData);
			
			if (SurfaceData.HitType == SurfaceData.TRACE_HitWall)
			{
				if (meta_developer)
						Console.printf("Bull Demon at %i x %i ain't got chargin' room", self.pos.x, self.pos.y);
					SetStateLabel("See");
			}
		}
		BULD A 0 A_SetPainThreshold(32);
		BULD A 0 { user_accelerator = 3; user_chargecooldown = 5; A_PlaySound("monsters/bulldemon/charge"); }
		BULD AB 4 A_FaceTarget;
		BULD A 0 A_Recoil(-user_accelerator-2);
		
		// full acceleration round 1
		BULD H 4 A_Recoil(-user_accelerator);
		BULD H 0 A_JumpIf(vel.x==0 && vel.y==0,"CrashedSomewhere");
		BULD H 0
		{
			user_accelerator = (user_accelerator + 2); 
			A_SpawnItemEx("BullDemonChargeFire",0,16,0);
			A_SpawnItemEx("BullDemonChargeFire",0,-16,0);
		}
		BULD H 4 A_Recoil(-user_accelerator);
		BULD H 0 A_JumpIf(vel.x==0 && vel.y==0,"CrashedSomewhere");
		BULD H 0 
		{
			user_accelerator = (user_accelerator + 2); 
			A_SpawnItemEx("BullDemonChargeFire",0,16,0);
			A_SpawnItemEx("BullDemonChargeFire",0,-16,0);
		}
		BULD H 4 A_Recoil(-user_accelerator);
		BULD H 0 A_JumpIf(vel.x==0 && vel.y==0,"CrashedSomewhere");
		BULD H 0 
		{
			user_accelerator = (user_accelerator + 2); 
			A_SpawnItemEx("BullDemonChargeFire",0,16,0);
			A_SpawnItemEx("BullDemonChargeFire",0,-16,0);
		}
		BULD H 4 A_Recoil(-user_accelerator);
		BULD H 0 A_JumpIf(vel.x==0 && vel.y==0,"CrashedSomewhere");
		BULD H 0 
		{
			user_accelerator = (user_accelerator + 2); 
			A_SpawnItemEx("BullDemonChargeFire",0,16,0);
			A_SpawnItemEx("BullDemonChargeFire",0,-16,0);
		}
		
		// full acceleration round 2
		BULD H 4 A_Recoil(-user_accelerator);
		BULD H 0 A_JumpIf(vel.x==0 && vel.y==0,"CrashedSomewhere");
		BULD H 0 
		{
			A_SpawnItemEx("BullDemonChargeFire",0,16,0);
			A_SpawnItemEx("BullDemonChargeFire",0,-16,0);
		}
		BULD H 4 A_Recoil(-user_accelerator);
		BULD H 0 A_JumpIf(vel.x==0 && vel.y==0,"CrashedSomewhere");
		BULD H 0 
		{
			A_SpawnItemEx("BullDemonChargeFire",0,16,0);
			A_SpawnItemEx("BullDemonChargeFire",0,-16,0);
		}
		BULD H 4 A_Recoil(-user_accelerator);
		BULD H 0 A_JumpIf(vel.x==0 && vel.y==0,"CrashedSomewhere");
		BULD H 0 
		{
			A_SpawnItemEx("BullDemonChargeFire",0,16,0);
			A_SpawnItemEx("BullDemonChargeFire",0,-16,0);
		}
		BULD H 4 A_Recoil(-user_accelerator);
		BULD H 0 A_JumpIf(vel.x==0 && vel.y==0,"CrashedSomewhere");
		BULD H 0 
		{
			A_SpawnItemEx("BullDemonChargeFire",0,16,0);
			A_SpawnItemEx("BullDemonChargeFire",0,-16,0);
		}
		
		//slowing down
		BULD H 4 A_JumpIf(vel.x==0 && vel.y==0,"CrashedSomewhere");
		BULD H 0 
		{
			A_ChangeVelocity(vel.x*0.8,vel.y*0.8,vel.z*0.8,CVF_REPLACE);
			A_SpawnItemEx("BullDemonChargeFire",0,16,0);
			A_SpawnItemEx("BullDemonChargeFire",0,-16,0);
		}
		BULD H 4 A_JumpIf(vel.x==0 && vel.y==0,"CrashedSomewhere");
		BULD H 0 
		{
			A_ChangeVelocity(vel.x*0.8,vel.y*0.8,vel.z*0.8,CVF_REPLACE);
			A_SpawnItemEx("BullDemonChargeFire",0,16,0);
			A_SpawnItemEx("BullDemonChargeFire",0,-16,0);
		}
		BULD H 4 A_JumpIf(vel.x==0 && vel.y==0,"CrashedSomewhere");
		BULD H 0 
		{
			A_ChangeVelocity(vel.x*0.8,vel.y*0.8,vel.z*0.8,CVF_REPLACE);
			A_SpawnItemEx("BullDemonChargeFire",0,16,0);
			A_SpawnItemEx("BullDemonChargeFire",0,-16,0);
		}
		BULD H 4 A_JumpIf(vel.x==0 && vel.y==0,"CrashedSomewhere");
		BULD H 0 
		{
			A_ChangeVelocity(vel.x*0.8,vel.y*0.8,vel.z*0.8,CVF_REPLACE);
			A_SpawnItemEx("BullDemonChargeFire",0,16,0);
			A_SpawnItemEx("BullDemonChargeFire",0,-16,0);
		}
		BULD H 0 A_SetPainThreshold(0);
		Goto See;
		
	CrashedSomewhere:
		BULD A 0 A_SetPainThreshold(0);
		BULD I 0 A_PlaySound("monsters/bulldemon/body",7,0.8,0/*,4.2*/);
		BULD I 0 A_SpawnItemEx("BullDemonDustCloud",20,0,32,0,0,0,0,SXF_NOCHECKPOSITION);
		BULD I 0 A_SpawnItemEx("BullDemonShockwave",20,0,32,0,0,0,0,SXF_NOCHECKPOSITION);
		BULD I 0 A_Explode(30,50,XF_NOTMISSILE,0,50);
		BULD I 20 A_Quake(sqrt(user_accelerator),user_accelerator/2,0,256);
		Goto See;
		
	Pain:
		BULD J 0;
		BULD J 0 A_SetPainThreshold(0);
		BULD J 2 Fast;
		BULD J 2 Fast A_Pain;
		Goto See;
	Punched:
		BULD J 3;
		BULD J 12 A_Pain;
		Goto See;
	Death:
		BULD J 0 { bSPRITEFLIP = random(0,1); } // random chance of sprite flipping
		TNT1 A 0 A_SpawnItem("SoulSpawner");
		BULD J 8 A_GiveToTarget("ScoreItem", 200);
		BULD K 8 A_Scream;
		BULD L 4 A_NoBlocking;
		BULD M 4 A_PlaySound("monsters/bulldemon/body");
		BULD N 4;
		BULD O -1;
		Stop;
	XDeath:
		TNT1 A 0 A_SpawnItem("SoulSpawner");
		BULD J 5 A_GiveToTarget("ScoreItem", 200);
		TNT1 A 0 A_SpawnItemEx("MetaGibber",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS);
		Stop;
	Raise:
		BULD O 5 { bSPRITEFLIP = 0; }
		BULD NMLKJ 5;
		Goto See;
	}
}

class BullDemonChargeFire : Actor
{
	Default
	{
		+NOINTERACTION
		+NOGRAVITY
		RenderStyle "Add";
		Alpha 1;
		scale 0.25;
	}

	States
	{
	Spawn:
		FIRE A 0;
		FIRE ABCDEH 2 BRIGHT;
		Stop;
	}
}

class BullDemonDustCloud : Actor
{
	Default
	{
		+NOINTERACTION
		+CLIENTSIDEONLY
		+FORCEXYBILLBOARD
		Renderstyle "Shaded";
		StencilColor "6b 54 32";
		Scale 1.4;
	}

	States
	{
	Spawn:
		SCRH A 1 
		{
			A_FadeOut(0.03);
			A_SetScale(scale.x + 0.1);
		}
		Wait;
	}
}

class BullDemonShockwave : Actor
{
	Default
	{
		radius 10;
		height 10;
		+NOCLIP
		+WALLSPRITE
		+NOGRAVITY
		+NOINTERACTION
		+ROLLCENTER
		Renderstyle "Add";
	}

	states
	{
	Spawn:
		SHOK A 0;
	DoTheThing:
		SHOK A 1
		{
			A_FadeOut(0.1);
			A_SetScale(scale.x + 0.1);
		}
		loop;
	}
}