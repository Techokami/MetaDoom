/*
----------------------------
MetaDoom
Actor Definitions
Chainsaw
----------------------------
*/

class MetaChainsawPickup : WeaponGiver
{
	Default
	{
		DropItem "MetaChainsaw";
		Inventory.PickupMessage "$GOTCHAINSAW";
		Inventory.PickupSound "weapons/chainsaw/get";
	}
	
	States
	{
	Spawn:
		WSAW Z 0;
		WSAW Z 0 { bDROPPED = 0; }
		WSAW Z -1;
		Stop;
	}
}

class MetaChainsaw : MetaDoomWeapon
{
	Default
	{
		Weapon.Kickback 0;
		Weapon.SelectionOrder 2300;
		Weapon.UpSound "weapons/sawup";
		//Weapon.ReadySound "weapons/sawidle"
		Inventory.PickupMessage "$GOTCHAINSAW";
		Obituary "$OB_METACHAINSAW";
		Tag "$TAG_CHAINSAW";
		+WEAPON.MELEEWEAPON		
		Inventory.PickupSound "weapons/chainsaw/get";
		Weapon.YAdjust 10;
		Weapon.UpSound "weapons/chainsaw/use";
		//$Category "Weapons/MetaDoom"
		//$Title "Chainsaw"
		//$NotAngled
		//$Color 14
	}
	
	States
	{
	Ready:
		WSAW A 0 A_PlaySound ("weapons/chainsaw/idle", CHAN_WEAPON, 1.0, 1);
		WSAW AB 1
		{
			A_WeaponReady(WRF_NOBOB);
			A_WeaponOffset(random(-1,1),random(32,33),WOF_INTERPOLATE);
			return A_JumpIfInventory("MeleePunch", 1, "Melee");
		}
		Loop;
	Deselect:
		WSAW A 0;
		WSAW A 0 A_JumpIf(health <= 0, "DeadDude");
		WSAW A 0 A_PlaySound("weapons/chainsaw/off", CHAN_WEAPON);
	FinishDeselect:
		WSAW A 1 A_SmoothLower();
		Loop;
	Select:
		WSAW AB 1 A_SmoothRaise();
		loop;
	Fire:
		WSAW C 0 
		{
			A_PlaySound ("weapons/chainsaw/attack", CHAN_WEAPON, 1.0, 1);
			A_Saw ("null","weapons/chainsaw/attackhit");
			
			if (CountInv("PowerDamage") > 0)
			{
				A_PlaySound("powerups/quad/use", CHAN_5);
			}
		}
		WSAW CDCD 1 A_WeaponOffset(random(-4,4),random(31,34),WOF_INTERPOLATE);
		WSAW C 0 A_ReFire;
		Goto Ready;
	AltFire:
		WSAW A 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		WSAW B 1 A_WeaponOffset(0,37,WOF_INTERPOLATE);
		WSAW A 1 A_WeaponOffset(0,40,WOF_INTERPOLATE);
		WSAW B 1 A_WeaponOffset(0,50,WOF_INTERPOLATE);
		WSAW A 3 A_WeaponOffset(0,60,WOF_INTERPOLATE);
		WSAW H 1 
		{
			A_WeaponOffset(0,32);
			A_StopSound(CHAN_WEAPON);
			A_PlaySound ("weapons/chainsaw/slash", CHAN_BODY);
			
			if (CountInv("PowerDamage") > 0)
			{
				A_PlaySound("powerups/quad/use", CHAN_5);
			}
			
		}
		WSAW I 1;
		WSAW J 1 A_FireProjectile("ChainsawSlash", -32, 0, 0, 0, FPF_NOAUTOAIM);
		WSAW K 1 A_FireProjectile("ChainsawSlash", -16, 0, 0, 0, FPF_NOAUTOAIM);
		WSAW L 1 A_FireProjectile("ChainsawSlash", 0, 0, 0, 0, FPF_NOAUTOAIM);
		WSAW M 1 A_FireProjectile("ChainsawSlash", 16, 0, 0, 0, FPF_NOAUTOAIM);
		WSAW N 1 A_FireProjectile("ChainsawSlash", 32, 0, 0, 0, FPF_NOAUTOAIM);
		TNT1 A 32 A_WeaponOffset(0,90);
		WSAW A 1 { A_WeaponOffset(0,90,WOF_INTERPOLATE); A_PlaySound("weapons/chainsaw/get", CHAN_WEAPON); }
		WSAW B 1 A_WeaponOffset(0,80,WOF_INTERPOLATE);
		WSAW A 1 A_WeaponOffset(0,70,WOF_INTERPOLATE);
		WSAW B 1 A_WeaponOffset(0,60,WOF_INTERPOLATE);
		WSAW A 1 A_WeaponOffset(0,55,WOF_INTERPOLATE);
		WSAW B 1 A_WeaponOffset(0,49,WOF_INTERPOLATE);
		WSAW A 1 A_WeaponOffset(0,44,WOF_INTERPOLATE);
		WSAW B 1 A_WeaponOffset(0,39,WOF_INTERPOLATE);
		WSAW A 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		WSAW B 1 A_WeaponOffset(0,33,WOF_INTERPOLATE);
		WSAW A 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		goto Ready;
	Melee:
		WSAW A 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		WSAW B 1 A_WeaponOffset(0,47,WOF_INTERPOLATE);
		WSAW A 1 A_WeaponOffset(0,55,WOF_INTERPOLATE);
		WSAW B 1 A_WeaponOffset(0,70,WOF_INTERPOLATE);
		WSAW A 3 A_WeaponOffset(0,80,WOF_INTERPOLATE);
		WSAW E 0 A_StopSound(CHAN_WEAPON);
		WSAW E 1 
		{
			A_WeaponOffset(1,65,WOF_INTERPOLATE);
			A_PlaySound ("weapons/chainsaw/slash", CHAN_BODY);
			
			if (CountInv("PowerDamage") > 0)
			{
				A_PlaySound("powerups/quad/use", CHAN_5);
			}
		}
		WSAW F 1 
		{
			A_WeaponOffset(0,42,WOF_INTERPOLATE);
			A_CustomPunch(5 * random(1, 5), TRUE, CPF_NOTURN,"MetaChainsawMeleePuff",80,0,0,"BasicArmorBonus","weapons/chainsaw/slashhit");
		}
		WSAW E 1 
		{
			A_WeaponOffset(-1,30,WOF_INTERPOLATE);
			A_CustomPunch(5 * random(1, 5), TRUE, CPF_NOTURN,"MetaChainsawMeleePuff",80,0,0,"BasicArmorBonus","weapons/chainsaw/slashhit");
		}
		WSAW F 1 
		{
			A_WeaponOffset(0,28,WOF_INTERPOLATE);
			A_CustomPunch(5 * random(1, 5), TRUE, CPF_NOTURN,"MetaChainsawMeleePuff",80,0,0,"BasicArmorBonus","weapons/chainsaw/slashhit");
		}
		WSAW E 1 
		{
			A_WeaponOffset(1,26,WOF_INTERPOLATE);
			A_CustomPunch(5 * random(1, 5), TRUE, CPF_NOTURN,"MetaChainsawMeleePuff",80,0,0,"BasicArmorBonus","weapons/chainsaw/slashhit");
		}
		WSAW FEFEFEFEFE 1 
		{
			A_WeaponOffset(random(-2,2),random(24,25),WOF_INTERPOLATE);
			A_CustomPunch(5 * random(1, 5), TRUE, CPF_NOTURN,"MetaChainsawMeleePuff",80,0,0,"BasicArmorBonus","weapons/chainsaw/slashhit");
		}
		WSAW F 1 A_WeaponOffset(0,26,WOF_INTERPOLATE);
		WSAW A 1 A_WeaponOffset(0,28,WOF_INTERPOLATE);
		WSAW A 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		WSAW A 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		WSAW A 1 A_WeaponOffset(0,39,WOF_INTERPOLATE);
		WSAW A 1 A_WeaponOffset(0,49,WOF_INTERPOLATE);
		WSAW A 1 A_WeaponOffset(0,60,WOF_INTERPOLATE);
		WSAW A 1 A_WeaponOffset(0,80,WOF_INTERPOLATE);
		TNT1 A 5 A_WeaponOffset(0,90,WOF_INTERPOLATE);
		WSAW A 1 { A_WeaponOffset(0,90,WOF_INTERPOLATE); A_PlaySound("weapons/chainsaw/get", CHAN_WEAPON); }
		WSAW B 1 A_WeaponOffset(0,80,WOF_INTERPOLATE);
		WSAW A 1 A_WeaponOffset(0,70,WOF_INTERPOLATE);
		WSAW B 1 A_WeaponOffset(0,60,WOF_INTERPOLATE);
		WSAW A 1 A_WeaponOffset(0,55,WOF_INTERPOLATE);
		WSAW B 1 A_WeaponOffset(0,49,WOF_INTERPOLATE);
		WSAW A 1 A_WeaponOffset(0,44,WOF_INTERPOLATE);
		WSAW B 1 A_WeaponOffset(0,39,WOF_INTERPOLATE);
		WSAW A 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		WSAW B 1 A_WeaponOffset(0,33,WOF_INTERPOLATE);
		WSAW A 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		goto Ready;
	Spawn:
		WSAW Z -1;
		Stop;
	}
}

class ChainsawSlash : Actor
{
	Default
	{
		Radius 13;
		Height 8;
		Speed 10;
		DamageFunction (3 * random(1, 4));
		Projectile;
		+RIPPER
		Obituary "$OB_METACHAINSAW_SLASH";
		alpha 0.5;
	}
	
	States
	{
	Spawn:
		CSLS A 8;
		stop;
	Death:
		TNT1 A 0;
		Stop;
	}
}

class MetaChainsawMeleePuff : Actor
{
	Default
	{
		+NOBLOCKMAP
		+NOGRAVITY
		+ALLOWPARTICLES
		+RANDOMIZE
		damagetype "ChainsawMelee";
		RenderStyle "Translucent";
		Alpha 0.5;
		VSpeed 1;
		Mass 5;
		//AttackSound "weapons/melee/hitwall"
		SeeSound "weapons/melee/hit";
		Obituary "$OB_METAFIST";
	}
	States
	{
	Spawn:
		PUFF A 4 Bright;
		PUFF B 4;
	Melee:
		PUFF CD 4;
		Stop;
	}
}