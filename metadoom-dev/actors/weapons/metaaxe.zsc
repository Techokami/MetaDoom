/*
----------------------------
MetaDoom
Actor Definitions
Fire Axe
----------------------------
*/

class MetaAxePickup : WeaponGiver
{
	Default
	{
		DropItem "MetaAxe";
		Inventory.PickupMessage "$GOTAXE";
		Inventory.PickupSound "weapons/axe/get";
	}
	States
	{
	Spawn:
		AXEG Z 0;
		AXEG Z 0 { bDROPPED = 0; }
		AXEG Z -1;
		Stop;
	}
}

class MetaAxe : MetaDoomWeapon
{
	Default
	{
		Weapon.SelectionOrder 2400;
		Weapon.Kickback 100;
		Obituary "$OB_AXE";
		Tag "$TAGAXE";
		+WEAPON.WIMPY_WEAPON
		+WEAPON.MELEEWEAPON
		+WEAPON.NOALERT
		Inventory.PickupMessage "$GOTAXE";
		Inventory.PickupSound "weapons/axe/get";
		Weapon.UpSound "weapons/axe/use";
		//$Category "Weapons/MetaDoom"
		//$Title "Fire Axe"
		//$NotAngled
		//$Color 14
	}
	
	States
	{
	Ready:
		AXEG A 1 A_WeaponReady;
		"####" A 0 A_JumpIfInventory("MeleePunch", 1, "MeleePunch");
		Loop;
	Deselect:
		AXEG A 0 A_JumpIf(health <= 0, "DeadDude");
		AXEG A 0 A_PlaySound("weapons/swap");
	FinishDeselect:
		AXEG A 1 A_SmoothLower();
		Loop;
	Select:
		AXEG A 0 A_OverlayScale(PSP_WEAPON, 1, 1);
		AXEG A 1 A_SmoothRaise();
		loop;
	AltFire:
	Fire:
		TNT1 A 0 A_PlaySound("weapons/swap", CHAN_AUTO, 0.5);
		AXEG BCDEFG 1;
		TNT1 A 2;
		AXEG I 1
		{
			A_PlaySound("weapons/axe/fire");
			
			if (CountInv("PowerMetaDamage") > 0)
			{
				A_PlaySound("powerups/quad/use", CHAN_5);
			}
		}
		AXEG JK 1;
		AXEG L 1 A_CustomPunch(7 * random(1, 5), TRUE, CPF_NOTURN,"MetaAxePuff",80,0,0,"BasicArmorBonus","weapons/axe/hit");
		AXEG MNO 1;
		TNT1 A 10 A_WeaponOffset(0,90);
		AXEG A 1 A_WeaponOffset(0,90,WOF_INTERPOLATE);
		AXEG A 1 A_WeaponOffset(0,70,WOF_INTERPOLATE);
		AXEG A 1 A_WeaponOffset(0,55,WOF_INTERPOLATE);
		AXEG A 1 A_WeaponOffset(0,44,WOF_INTERPOLATE);
		AXEG A 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		AXEG A 1 A_WeaponOffset(0,33,WOF_INTERPOLATE);
		AXEG A 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		Goto Ready;
	Spawn:
		AXEG Z -1;
		Stop;
	}
}

class MetaAxePuff : Actor
{
	Default
	{
		+NOBLOCKMAP
		+NOGRAVITY
		+ALLOWPARTICLES
		+RANDOMIZE
		damagetype "Axe";
		RenderStyle "Translucent";
		Alpha 0.5;
		VSpeed 1;
		Mass 5;
		AttackSound "weapons/axe/hitwall";
		SeeSound "weapons/axe/hit";
		Obituary "$OB_AXE";
	}
	States
	{
	Spawn:
		PUFF A 4 Bright;
		PUFF B 4;
	Melee:
		PUFF CD 4;
		Stop;
	}
}