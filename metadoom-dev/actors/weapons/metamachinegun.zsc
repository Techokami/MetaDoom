/*
----------------------------
MetaDoom
Actor Definitions
Assault Rifle
----------------------------
*/

class MachinegunRockets: _Boolean {}
class MachinegunCheat: _Boolean {}

class MetaMachineGunPickup : WeaponGiver
{
	Default
	{
		DropItem "MetaMachineGun";
		Inventory.PickupMessage "$GOTMACGUN";
		Inventory.PickupSound "weapons/machinegun/get";
	}
	
	States
	{
	Spawn:
		MACG Z 0;
		MACG Z 0 { bDROPPED = 0; }
		MACG Z -1;
		Stop;
	}
}

class MetaMachineGun : MetaDoomWeapon
{
	Default
	{
		Weapon.SelectionOrder 700;
		Weapon.AmmoGive 20;
		Weapon.AmmoUse1 1;
		//Weapon.AmmoUse2 5;
		Weapon.AmmoType1 "Clip";
		Weapon.AmmoType2 "Clip";
		Inventory.PickupMessage "$GOTMACGUN";
		Obituary "$OB_METAMG_BULLET";
		Tag "$TAGMACGUN";
		Inventory.PickupSound "weapons/machinegun/get";
		+WEAPON.ALT_AMMO_OPTIONAL
		+WEAPON.NOALERT
		Weapon.UpSound "weapons/machinegun/use";
		//$Category "Weapons/MetaDoom"
		//$Title "Assault Rifle"
		//$NotAngled
		//$Color 14
	}
	
	States
	{
	Ready:
		MACG A 0 A_JumpIfInventory("MachinegunRockets",1,"AltfireReady");
		MACG A 1 A_WeaponReady;
		"####" A 0 A_JumpIfInventory("MeleePunch", 1, "MeleePunch");
		Loop;
	AltfireReady:
		MACG J 1 A_WeaponReady;
		MACG J 0 A_JumpIfInventory("MeleePunch", 1, "MeleePunch");
		Loop;
	Deselect:
		MACG A 0;
		MACG A 0 A_JumpIf(health <= 0, "DeadDude");
		MACG A 0 A_JumpIfInventory("MachinegunRockets",1,"AltfireDeselect");
		MACG A 0 A_PlaySound("weapons/swap");
	FinishDeselect:
		MACG A 1 A_SmoothLower();
		Loop;
	Select:
		MACG A 0 A_OverlayScale(PSP_WEAPON, 1, 1);
		MACG A 1 A_SmoothRaise();
		loop;
	Fire:
		MACG A 0 A_JumpIfInventory("MachinegunRockets",1,"Altfirefire");
		MACG B 2 bright
		{
			A_PlaySound("weapons/machinegun/fire", CHAN_WEAPON);
			
			if (CountInv("PowerMetaDamage") > 0)
			{
				A_PlaySound("powerups/quad/use", CHAN_5);
			}
			
			A_GunFlash();
			A_FireBullets(5.6, 0, 1, 5, "MetaBulletPuff");
			A_AlertMonsters();
			A_Overlay(14, "Overlay.Recoil");
		}
		MACG C 2 bright
		{
			if (meta_perf_spentbrass == 1)
			{
				A_FireProjectile("BulletCasingSpawn",5,0,6,4);
			}
		}
		MACG D 0;
		MACG D 0 A_ReFire;
		Goto Ready;
	Altfirefire:
		MACG J 0 A_JumpIf(CountInv("Clip") < 5, "AltfireEnd");
		MACG K 2 bright
		{
			A_PlaySound("weapons/machinegun/missile", CHAN_WEAPON);
			
			if (CountInv("PowerMetaDamage") > 0)
			{
				A_PlaySound("powerups/quad/use", CHAN_5);
			}
			
			A_GunFlash();
			A_AlertMonsters();
			A_Overlay(14, "Overlay.AltRecoil");
			
			if (CountInv("MachinegunCheat") > 0)
			{
				A_FireProjectile("MetaShotgunGrenade");
			} else {
				A_FireProjectile("MicroRocket");
			}
			A_TakeInventory("Clip", 5);
		}
		MACG LM 2;
		MACG J 0 A_ReFire;
		Goto Ready;
	Altfire:
		MACG E 0 A_JumpIfInventory("MachinegunRockets",1,"AltfireEnd");
	AltfireStart:
		MACG E 0 A_JumpIf(CountInv("Clip") < 5, "AltfireBlank");
		MACG E 0 A_PlaySound("weapons/machinegun/rackout");
		MACG EFGHIJ 2;
		MACG J 5 A_GiveInventory("MachinegunRockets", 1);
		goto Ready;
	AltfireEnd:
		MACG J 0 A_PlaySound("weapons/machinegun/rackin");
		MACG IHGFE 2;
		MACG A 0 A_TakeInventory("MachinegunRockets", 999);
		Goto Ready;
	AltfireDeselect:
		MACG J 0 A_PlaySound("weapons/machinegun/rackin");
		MACG IHGFE 2;
		MACG A 0 A_TakeInventory("MachinegunRockets", 999);
		Goto Deselect;
	AltfireBlank:
		MACG A 1 Offset(0,34) A_PlaySound("weapons/machinegun/dry", CHAN_BODY);
		MACG A 1 Offset(0,33);
		MACG A 1 Offset(0,32) A_TakeInventory("MachinegunRockets", 1);
		MACG A 10;
		goto Ready;
	Flash:
		TNT1 A 5 Bright A_Light1;
		Goto LightDone;
		TNT1 A 5 Bright A_Light2;
		Goto LightDone;
	Spawn:
		MACG Z -1;
		Stop;
		
	Overlay.Recoil:
		TNT1 D 1 A_WeaponOffset(0,34,WOF_INTERPOLATE);
		TNT1 D 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		TNT1 E 1 A_WeaponOffset(0,33,WOF_INTERPOLATE);
		TNT1 E 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		TNT1 A -1;
		stop;
	Overlay.AltRecoil:
		TNT1 B 1 A_WeaponOffset(0,38,WOF_INTERPOLATE);
		TNT1 C 1 A_WeaponOffset(0,42,WOF_INTERPOLATE);
		TNT1 C 1 A_WeaponOffset(0,43,WOF_INTERPOLATE);
		TNT1 D 1 A_WeaponOffset(0,38,WOF_INTERPOLATE);
		TNT1 D 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		TNT1 E 1 A_WeaponOffset(0,33,WOF_INTERPOLATE);
		TNT1 E 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		TNT1 A -1;
		stop;
	}
}

class MicroRocket : Actor
{
	Default
	{
		Radius 8;
		Height 8;
		Speed 40;
		Damage 5;
		Projectile;
		Obituary "$OB_METAMG_MISSILE";
		scale 0.5;
		Decal "RevenantScorch";
	}
	
	States
	{
	Spawn:
		MISL A 1 Bright;
		Loop;
	Death:
		MISL A 4 Bright;
		TNT1 A 0 A_SpawnItemEx("MicroRocketExplosion",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS);
		Stop;
	}
}

class MicroRocketExplosion : MetaExplosion
{
	Default
	{
		scale 0.3;
		Obituary "$OB_METAMG_MISSILE";
	}
	
	States
	{
		Spawn:
		EXPL A 0;
		EXPL A 2 BRIGHT
		{
			A_PlaySound("weapons/shotgun/grenade", CHAN_WEAPON);
			A_Explode(32,64);
			A_Quake (1,3,0,750,0);
		}
		EXPL B 2 BRIGHT;
		EXPL C 2 BRIGHT;
		EXPL DEF 2;
		Stop;
	}
}