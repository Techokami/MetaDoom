/*
----------------------------
MetaDoom
Actor Definitions
Fists
----------------------------
*/

// The Last Resort - if you lose your pistol somehow, this is given to you to
// give you a fighting chance.
class MetaLastResortFist : MetaDoomWeapon
{
	Default
	{
		Weapon.SelectionOrder 3700;
		Weapon.Kickback 100;
		Obituary "$OB_BERSERK";
		Tag "$TAG_FIST";
		+WEAPON.WIMPY_WEAPON
		+WEAPON.MELEEWEAPON
		+WEAPON.NOALERT
	}
	
	States
	{
	Ready:
		PUNG A 1 A_WeaponReady;
		"####" A 0 A_JumpIfInventory("MeleePunch", 1, "MeleePunch");
		Loop;
	Deselect:
		PUNG A 0 A_JumpIf(health <= 0, "DeadDude");
		PUNG A 0 A_PlaySound("weapons/swap");
	FinishDeselect:
		PUNG A 1 A_SmoothLower();
		Loop;
	Select:
		goto Ready;
	AltFire:
	Fire:
		TNT1 A 0;
		goto MeleePunch;
	}
}

// Insert your own Love Among Freaks reference here.
class MetaBerserkFist : MetaDoomWeapon
{
	Default
	{
		Weapon.SelectionOrder 3700;
		Weapon.Kickback 100;
		Obituary "$OB_BERSERK";
		Tag "$TAG_FIST";
		+WEAPON.WIMPY_WEAPON
		+WEAPON.MELEEWEAPON
		+WEAPON.NOALERT
		Weapon.UpSound "weapons/swap";
	}
	
	States
	{
	Ready:
		PUNG A 1 A_WeaponReady;
		"####" A 0 A_JumpIfInventory("MeleePunch", 1, "MeleePunch");
		Loop;
	Deselect:
		PUNG A 0 A_JumpIf(health <= 0, "DeadDude");
		PUNG A 0 A_PlaySound("weapons/swap");
	FinishDeselect:
		PUNG A 1 A_SmoothLower();
		Loop;
	Select:
		PUNG A 1 A_SmoothRaise();
		loop;
	AltFire:
	Fire:
		TNT1 A 0 
		{
			A_PlaySound("weapons/melee/wiff");
			
			if (CountInv("PowerDamage") > 0)
			{
				A_PlaySound("powerups/quad/use", CHAN_5);
			}
		}
		TNT1 A 0 A_Jump(128, "FireRight");
		goto FireLeft;
	FireLeft:
		PUNL B 1;
		PUNL DF 1;
		PUNL G 1 A_CustomPunch(60 * random(1, 3), TRUE, CPF_NOTURN,"MetaMeleePuff",80,0,0,"BasicArmorBonus","weapons/melee/hit");
		PUNL IKMOQ 1;
		TNT1 A 6;
		Goto Ready;
	FireRight:
		PUNR B 1;
		PUNR DF 1;
		PUNR G 1 A_CustomPunch(60 * random(1, 3), TRUE, CPF_NOTURN,"MetaMeleePuff",80,0,0,"BasicArmorBonus","weapons/melee/hit");
		PUNR IKMOQ 1;
		TNT1 A 6;
		Goto Ready;
	}
}