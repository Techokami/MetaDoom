/*
----------------------------
MetaDoom
Actor Definitions
Armor Pickups
----------------------------
*/

// Armor bonuses occasionally spawn loot. See metaloot.zsc
class MetaArmorBonus : BasicArmorBonus replaces ArmorBonus
{
	Default
	{
		Radius 20;
		Height 16;
		Inventory.Pickupmessage "$GOTARMBONUS";
		Inventory.Icon "BON2A0";
		Armor.Savepercent 33.335;
		Armor.Saveamount 1;
		Armor.Maxsaveamount 200;
		Inventory.PickupSound "items/armor/shard";
		Scale 0.66;
		+COUNTITEM;
		+INVENTORY.ALWAYSPICKUP;
		TeleFogSourceType "MetaItemFog";
		TeleFogDestType "MetaItemFog";
	}
	
	States
	{
	Spawn:
		SHRD A 0;
		SHRD A 0 A_JumpIf(meta_lootspawns == 0, 2);
		SHRD A 0 A_SpawnItem("LootSpawner");
		SHRD A 0;
	DoALoop:
		SHRD A 6 BRIGHT;
		SHRD BCB 4;
		loop;
	}
}

class MetaArmorBonusNoItemSpawn : MetaArmorBonus
{
	Default
	{
		//$Category "Health and Armor/MetaDoom"
		//$Title "Armor Bonus (No Item Spawn Chance)"
		//$NotAngled
		//$Color 1
	}
	
	States
	{
	Spawn:
		SHRD A 0;
		goto DoALoop;
	}
}

class MetaGreenArmor : BasicArmorPickup replaces GreenArmor
{
	Default
	{
		Radius 20;
		Height 16;
		Inventory.Pickupmessage "$GOTARMOR";
		Inventory.Icon "GARMA0";
		Inventory.PickupSound "items/armor/green";
		Armor.SavePercent 33.335;
		Armor.SaveAmount 100;
		TeleFogSourceType "MetaItemFog";
		TeleFogDestType "MetaItemFog";
	}
	
	States
	{
	Spawn:
		GARM A 6 bright;
		GARM BCB 4;
		loop;
	}
}

// Blue Armor has a 25% chance of spawning Flame Belch.
// This may be moved elsewhere?
class MetaBlueArmor : BasicArmorPickup replaces BlueArmor
{
	Default
	{
		Radius 20;
		Height 16;
		Inventory.Pickupmessage "$GOTMEGA";
		Inventory.Icon "BARMA0";
		Inventory.PickupSound "items/armor/blue";
		Armor.Savepercent 50;
		Armor.Saveamount 200;
		Scale 0.66;
		TeleFogSourceType "MetaItemFog";
		TeleFogDestType "MetaItemFog";
	}
	
	mixin LootSpawn;
	
	States
	{
	Spawn:
		BARM A 0;
		BARM A 0 A_Jump(64, "SpawnInv");
		goto Chill;
	SpawnInv:
		BARM A 0 A_Jump(256, "FlameBelch");
		goto Chill;
	FlameBelch:
		BARM A 0 A_SpawnLoot("FlameBelchPickup");
		goto Chill;
	Chill:
		BARM A 6 bright;
		BARM BCB 4;
		loop;
	}
}