/*
----------------------------
MetaDoom
Actor Definitions
Health Pickups
----------------------------
*/

// Health bonuses occasionally spawn loot. See metaloot.zsc
class MetaHealthBonus : Health replaces HealthBonus
{
	Default
	{
		+COUNTITEM
		+INVENTORY.ALWAYSPICKUP
		Inventory.Amount 1;
		Inventory.MaxAmount 200;
		Inventory.PickupMessage "$GOTHTHBONUS";
		Inventory.PickupSound "items/health/shard";
		TeleFogSourceType "MetaItemFog";
		TeleFogDestType "MetaItemFog";
	}
	
	States
	{
	Spawn:
		HCRM A 0;
		HCRM A 0 A_JumpIf(meta_lootspawns == 0, 2);
		HCRM A 0 A_SpawnItem("LootSpawner");
		HCRM A 0;
	DoALoop:
		HCRM ABCDCB 4;
		Loop;
	}
}

class MetaHealthBonusNoItemSpawn : MetaHealthBonus
{
	Default
	{
		//$Category "Health and Armor/MetaDoom"
		//$Title "Health Bonus (No Item Spawn Chance)"
		//$NotAngled
		//$Color 1
	}
	
	States
	{
	Spawn:
		BON1 A 0;
		goto DoALoop;
	}
}

class MetaStimpack : Health replaces Stimpack
{
	Default
	{
		Inventory.Amount 10;
		Inventory.PickupMessage "$GOTSTIM";
		Inventory.PickupSound "items/health/stim";
		TeleFogSourceType "MetaItemFog";
		TeleFogDestType "MetaItemFog";
	}
	
	States
	{
	Spawn:
		STMP A -1;
		Stop;
	}
}

// Medikits have a 6.25% chance of spawning a Shield Wall
class MetaMedikit : Health replaces Medikit
{
	Default
	{
		Inventory.Amount 25;
		Inventory.PickupMessage "$GOTMEDIKIT";
		Health.LowMessage 25, "$GOTMEDINEED";
		Inventory.PickupSound "items/health/medi";
		TeleFogSourceType "MetaItemFog";
		TeleFogDestType "MetaItemFog";
		Scale 0.5;
	}
	
	mixin LootSpawn;
	
	States
	{
	Spawn:
		MKIT A 0;
		MKIT A 0 A_Jump(16, "SpawnInv");
		goto Chill;
	SpawnInv:
		MKIT A 0 A_Jump(256, "ShieldWall");
		goto Chill;
	ShieldWall:
		MKIT A 0 A_SpawnLoot("ShieldWallPickup");
		goto Chill;
	Chill:
		MKIT A -1;
		Stop;
	}
}

// Alternate Medikit with no item spawn chance, used by Quad Damage
class MetaMedikitNoItemSpawn : MetaMedikit
{
	Default
	{
		+BLOCKASPLAYER
		BounceType "Grenade";
		BounceFactor 0.0;
		WallBounceFactor 1.0;
		//$Category "Health and Armor/MetaDoom"
		//$Title "Medikit (No Item Spawn Chance)"
		//$NotAngled
		//$Color 1
	}
	
	States
	{
	Spawn:
		MKIT A 0;
		goto Chill;
	}
}