/*
----------------------------
MetaDoom
ZScript Definitions
Replacement Scripts
----------------------------
*/

// Story time, kiddies!

// OK, SO... Originally, breakable props and WolfSS/Keen replacement were kept
// aside as a seperate add-on to avoid messing with mapsets that replace them
// via the curse that is DEHACKED. As of v5, it was incorporated into the main
// mod and toggled via a CVar. So far, so good, right?

// Well, now, thanks to Boondorl figuring out the untold secrets of state-specific
// flags like bDEHACKED, we no longer need a CVar as we can automatically detect
// when an actor has been touched by the darkness of the MS-DOS era of modding and
// not replace the actor as appropriate. As such, it's been expanded to cover
// pretty much ALL monsters to allow for better compatibility with some mapsets
// like Eviternity that replace vanilla monsters like the Baron.

// This was originally based on some really confusingly-formatted code by Matt
// (https://forum.zdoom.org/viewtopic.php?f=122&t=60916&p=1058774) but now it
// uses engine-provided functionality to do the same thing, just less grossly

////////////////////////////////////////////////////////////////////////////////

// Replace environment props with louder, breakable props
class MetaOptionalProps : EventHandler
{
	bool safetylock;

	// Check whether a DEHACKED lump has messed with frame names in a way we
	// can't detect any other way, and completely shut down prop replacements
	// if that turns out to be the case (see: Demonastery)
	// Code loosely based on Beautiful Doom's DEHACKED detector by 3saster.
    override void OnRegister()
	{
		if (meta_optional_props)
		{
			string data = Wads.ReadLump(Wads.FindLump("DEHACKED",0,1));
			// Delete comments
			while(data.IndexOf("#") != -1)
			{
				int start = data.IndexOf("#");
				int end   = data.IndexOf("\n",start);
				data.Remove(start,end-start);
			}
			// Split Lines
			Array<String> lines;
			data.Split(lines, "\n");
			
			// Parse each line
			for(int i=0; i<lines.Size(); i++)
			{
				if (lines[i].left(8) ~== "Text 4 4" && !safetylock)
				{
					Console.printf("Optional: DEHACKED lump is fucking with frame names, prop replacements disabled.");
					safetylock = true;
					break;
				}
			}
		}
	}
	
	override void CheckReplacement(ReplaceEvent e)
	{
		static const string oldActor[] =
		{
			// Gore
			"Gibs", "BrainStem", "ColonGibs", "SmallBloodPool", 
			"BloodyTwitch", "NonsolidTwitch", "DeadStick", "LiveStick", 
			"Meat2", "Meat3", "Meat4", "Meat5", 
			"NonsolidMeat2", "NonsolidMeat3", "NonsolidMeat4", "NonsolidMeat5", 
			"HangBNoBrain", "HangNoGuts", "HangTLookingDown", "HangTLookingUp", 
			"HangTNoBrain", "HangTSkull", 
			// Hell
			"FloatingSkull","HeartColumn","RedTorch","BlueTorch",
			"GreenTorch","ShortRedTorch","ShortBlueTorch","ShortGreenTorch",
			"BigTree","TorchTree","Candelabra","Candlestick",
			"HeadCandles","EvilEye","HeadOnAStick","HeadsOnAStick",
			// Tech
			"TechLamp","TechLamp2","Column","BurningBarrel","TechPillar",
			// Corpses
			"DeadCacodemon", "DeadDemon", "DeadDoomImp", "DeadMarine",
			"DeadZombieman", "DeadShotgunGuy", "GibbedMarine"
		};
		
		static const string newActor[] =
		{
			// Gore
			"MetaCrushedProp", "MetaBrainStem", "MetaColonGibs", "MetaSmallBloodPool", 
			"MetaBloodyTwitch", "MetaNonsolidTwitch", "MetaDeadStick", "MetaLiveStick", 
			"MetaMeat2", "MetaMeat3", "MetaMeat4", "MetaMeat5", 
			"MetaNonsolidMeat2", "MetaNonsolidMeat3", "MetaNonsolidMeat4", "MetaNonsolidMeat5", 
			"MetaHangBNoBrain", "MetaHangNoGuts", "MetaHangTLookingDown", "MetaHangTLookingUp", 
			"MetaHangTNoBrain", "MetaHangTSkull", 
			// Hell
			"MetaFloatingSkull","MetaHeartColumn","MetaRedTorch","MetaBlueTorch",
			"MetaGreenTorch","MetaShortRedTorch","MetaShortBlueTorch","MetaShortGreenTorch",
			"MetaBigTree","MetaTorchTree","MetaCandelabra","MetaCandlestick",
			"MetaHeadCandles","MetaEvilEye","MetaHeadOnAStick","MetaHeadsOnAStick",
			// Tech
			"MetaTechLamp","MetaTechLamp2","MetaColumn","MetaBurningBarrel","MetaTechPillar",
			// Corpses
			"MetaDeadCacodemon", "MetaDeadPinky", "MetaDeadDoomImp", "MetaDeadMarine",
			"MetaDeadZombieman", "MetaDeadShotgunGuy", "MetaGibbedMarine"
			
		};
		
		if (meta_optional_props && !safetylock)
		{
			for(int i = 0; i < oldActor.size(); i++)
			{
				if ( e.Replacee == oldActor[i] ) 
				{
					// Don't replace actors if they're already being replaced by DECORATE or ZScript.
					if (e.Replacement != null)
					{
						if (meta_developer)
							Console.printf("Optional: %s replaced by %s instead of %s", oldActor[i], e.Replacement.GetClassName(), newActor[i]);
						break;
					}
					
					// Don't replace actor if the vanilla actor has been altered by DEHACKED.
					if (GetDefaultByType(e.Replacee).SpawnState.bDeHacked
					|| (GetDefaultByType(e.Replacee).SeeState != null && GetDefaultByType(e.Replacee).SeeState.bDeHacked)
					|| (GetDefaultByType(e.Replacee).MeleeState != null && GetDefaultByType(e.Replacee).MeleeState.bDeHacked)
					|| (GetDefaultByType(e.Replacee).MissileState != null && GetDefaultByType(e.Replacee).MissileState.bDeHacked)
					|| (GetDefaultByType(e.Replacee).FindState("Pain") != null && GetDefaultByType(e.Replacee).FindState("Pain").bDeHacked)
					|| (GetDefaultByType(e.Replacee).FindState("Death") != null && GetDefaultByType(e.Replacee).FindState("Death").bDeHacked)
					|| (GetDefaultByType(e.Replacee).FindState("XDeath") != null && GetDefaultByType(e.Replacee).FindState("XDeath").bDeHacked))
					{
						if (meta_developer)
							Console.printf("Optional: %s is DEHACKED, not replacing with %s", oldActor[i], newActor[i]);
							break;
					} else {
						e.Replacement = newActor[i];
					}
				}
			}
		}
	}
}

// Replace monsters that haven't been replaced by DEHACKED with new, fully meta-fied monsters.
class MetaOptionalMonsters : EventHandler
{
    override void CheckReplacement(ReplaceEvent e)
	{
		static const string oldActor[] =
		{
			// Standard Doom Actors
			"ZombieMan", "ShotgunGuy", "ChaingunGuy", "DoomImp", "Demon", 
			"MBFHelperDog", "Spectre", "Cacodemon", "HellKnight", "PainElemental", 
			"LostSoul", "ArchVile", "Revenant", "BaronOfHell", "Arachnotron", "Fatso",
			"SpiderMastermind", "Cyberdemon",
			// Traditionally "Optional" Actors
			"WolfensteinSS", "CommanderKeen",
			// Scripted Marines - Order is important!
			"MarineRocket", "MarineBerserk", "MarineFist", "MarineChainsaw",
			"MarinePistol", "MarineShotgun", "MarineSSG", "MarineChaingun",
			"MarinePlasma", "MarineRailgun", "MarineBFG", "ScriptedMarine"
		};
		
		static const string newActor[] =
		{
			// Standard Doom Actors
			"ZombiemanSpawner", "ShotgunGuySpawner", "ChaingunGuySpawner", "ImpSpawner", "PinkySpawner",
			"DogSpawner", "SpectreSpawner", "CacodemonSpawner", "HellKnightSpawner", "PainElementalSpawner",
			"LostSoulSpawner", "ArchVileSpawner","RevenantSpawner",	"BaronOfHellSpawner", "ArachnotronSpawner", "MancubusSpawner",
			"SpiderMastermindSpawner", "CyberdemonSpawner",
			// Traditionally "Optional" Actors
			"WolfSSSpawner", "KeenSpawner",
			// Scripted Marines - Order is important!
			"MetaMarineRocket", "MetaMarineBerserk", "MetaMarineFist", "MetaMarineChainsaw",
			"MetaMarinePistol", "MetaMarineShotgun", "MetaMarineSSG", "MetaMarineChaingun",
			"MetaMarinePlasma", "MetaMarineRailgun", "MetaMarineBFG", "MetaScriptedMarine"			
		};
		
		for(int i = 0; i < oldActor.size(); i++)
		{
			if ( e.Replacee == oldActor[i] ) 
			{
				if (GetDefaultByType(e.Replacee).SpawnState.bDeHacked
				|| (GetDefaultByType(e.Replacee).SeeState != null && GetDefaultByType(e.Replacee).SeeState.bDeHacked)
				|| (GetDefaultByType(e.Replacee).MeleeState != null && GetDefaultByType(e.Replacee).MeleeState.bDeHacked)
				|| (GetDefaultByType(e.Replacee).MissileState != null && GetDefaultByType(e.Replacee).MissileState.bDeHacked)
				|| (GetDefaultByType(e.Replacee).FindState("Pain") != null && GetDefaultByType(e.Replacee).FindState("Pain").bDeHacked)
				|| (GetDefaultByType(e.Replacee).FindState("Death") != null && GetDefaultByType(e.Replacee).FindState("Death").bDeHacked)
				|| (GetDefaultByType(e.Replacee).FindState("XDeath") != null && GetDefaultByType(e.Replacee).FindState("XDeath").bDeHacked))
				{
					if (meta_developer)
						Console.printf("Optional: %s is DEHACKED, not replacing with %s", oldActor[i], newActor[i]);
						break;
				} else {
					e.Replacement = newActor[i];
				}
			}
		}
    }
	
	// Un-Fuck A_BossDeath
	override void CheckReplacee(ReplacedEvent e)
	{
		if (e.Replacement == "BaronOfHellSpawner")
		{
			e.Replacee = "BaronOfHell";
		}
		
		if (e.Replacement == "MancubusSpawner")
		{
			e.Replacee = "Fatso";
		}
		
		if (e.Replacement == "ArachnotronSpawner")
		{
			e.Replacee = "Arachnotron";
		}
		
		if (e.Replacement == "CyberdemonSpawner")
		{
			e.Replacee = "Cyberdemon";
		}
		
		if (e.Replacement == "SpiderMastermindSpawner")
		{
			e.Replacee = "SpiderMastermind";
		}
	}
}